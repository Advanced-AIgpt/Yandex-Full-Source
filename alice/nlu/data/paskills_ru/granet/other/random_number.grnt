import: common/filler.grnt

# префикс YANDEXTEST тут специально чтобы не пересекаться со встроенными грамматиками
form YANDEXTEST.RANDOM_NUMBER_SAMPLE:
    slots:
        lower_bound:
            source: $LowerBound
            type: string
        upper_bound:
            source: $UpperBound

            type: string
root:
    $Prefix* $GiveMeNumber
    $Prefix* [$GiveMeNumber? $FullRange]
    $Prefix* [$GiveMeNumber $PartialRange]
$FullRange:
    от $LowerBound до $UpperBound
    между $LowerBound и $UpperBound
$PartialRange:
    до $UpperBound
    больше $LowerBound
    меньше $UpperBound
$LowerBound:
    $YANDEX.NUMBER
$UpperBound:
    $YANDEX.NUMBER
$GiveMeNumber:
    $Number
    [$Again $Number]
    [$Give $Me? $Again? $Number]
    [можешь $ToGive $Me? $Again? $Number]
$Give:
    выбери | выдай | дай | назови | скажи | загадай
    мне нужно
    можешь? $ToGive
$ToGive:
    выбрать | выдать | дать | назвать | сказать | загадать
$Me:
    мне | нам
$Again:
    eще одно?
    eще один? (раз|разок)?
$Number:
    одно? $Any? число
$Any:
    какое-нибудь | случайное | любое | произвольное | рандомное
$Prefix:
    $Common.LeftFiller
filler:
    $Common.Filler
    сейчас | короче | быстро | теперь
