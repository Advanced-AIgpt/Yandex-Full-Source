import: common/filler.grnt

# Deprecated. Use same form from ru/
form paskills.internal.deactivate_weak:
    root:
        $AnotherGame
        $Rude+
        $WhatTimeIsIt
        $TurnOff+
        $StopGame+
        $MusicOrRadio
        $ImTired
        $GoodNight
        $Weather

    $Rude:
        сука
        иди (на хуй | в жопу)
        ты (тупая | глупая)

    $WhatTimeIsIt:
        сколько сейчас? времени
        сколько время
        который час

    $TurnOff:
        %lemma
        выключить
        выключись
        выйти
        прекратить
        остановись
        останови
        стоп
        замолчи
        заткнись
        хватит
        все
        отстань
        отмена
        закончим
        завершить
        перестань
        перезагрузись
        конец

    $GameOrPlay:
        %lemma
        игра
        навык
        играть
        поиграть
        сыграть

    $Want:
        хочу
        хотим
        буду
        будем

    $StopGame:
        %lemma
        [$TurnOff $GameOrPlay] .*
        не будем играть .*
        [больше? (не $Want?)] $GameOrPlay .*
        надоело $GameOrPlay

    $Another:
        %lemma
        другую
        что-нибудь другое
        что нибудь другое
        какую-нибудь другую
        какую нибудь другую

    $AnotherGame:
        [$Another $GameOrPlay+]

    $TurnOn:
        включи
        вруби
        поставь

    $MusicAdjective:
        %lemma
        детские
        спокойную
        веселую

    $MusicContentType:
        %lemma
        музыка
        музон
        радио
        песня
        трек

    $MusicOrRadio:
        $TurnOn? $MusicAdjective? $MusicContentType .*
        $TurnOff $MusicContentType

    $GoodNight:
        спокойной ночи

    $ImTired:
        %lemma
        (я|мы)? устал

    $WeatherModifier:
        какая
        завтра
        сейчас
        будет
        сегодня

    $Weather:
        $WeatherModifier* погода .*
        (расскажи|скажи) погоду .*

    filler:
        $Common.Filler
        $Common.LeftFiller
        в
        во
        я
        мы
        как
        из
