import:
    common/commands.grnt
    common/filler.grnt
    route_manager/common.grnt


form alice.route_manager.continue:
    root:
        $Continue
    filler:
        $nonsense
        $Common.WayToCallAlice
        $Common.Filler
        $Common.PronounMe
        побыстрее
        отбой
        извини
        за
        больше
        нет
        можем|можно
        все
        на месте
        готов|готова
        который|которая|которое
        был|была|было
        ну что
        в принципе
        если
        такси
        
$Continue:
    [$CancelStop* $Common.Lets? $Return? ($ContinueVerbs|$StartCommands|$ExtraRideVerbs)+ по? $RideDescription* ($Ride|$Further)+]
    [$CancelStop* $Common.Lets? $Return ($LocalRide|$OnRoute)+ $Further*]
    [$Cancel (поиск|искать)? $Parking]
    $SingleStartCommands+
    
    $Further:
        %lemma
        далее
        дальше
        до конечной остановки?
        до конца
        (по|до|к|в) $PlaceDescription? $Place
        как ехали
        $OnRoute
    
    $LocalRide:
        $Ride
        %force_negative
        маршрут

    $OnRoute:
        %lemma
        (по|в|на|к) ($RideDescription|$Previous)* $Route

    $Previous:
        %lemma
        прежний
        изначальный
        исходный
        первоначальный

    $CancelStop:
        %lemma
        [$Cancel (поиск|искать)? $Parking?]
        [$Cancel $StopVerbs]
        [$Cancel $StopRideVerbs]
        [$Cancel $ContinueVerbs? (искать|ищи)? $Parking]
        передумать
        ошибся
        без $Parking
        спасибо за $Parking
        [$Parking не требуется]

    $Cancel:
        %lemma
        $Common.Cancel
        отменяем
        хватит
        передумал
        заверши
        отменяется
        прекращаем
        закончить
        остановить
        не $Common.ToWant?
    
    $Return:
        $Inner:
            %lemma
            $Common.ReturnTo
            вернемся
            возвращаемся
        $Inner (к|на|в)?

    $Parking:
        %lemma
        $Inner:
            %lemma
            остановка
            стоянка
            парковка
        $Inner
        [место для? $Inner]
        парковочное место

    $ExtraRideVerbs:
        %lemma
        доедем
        следуем
        выполняй
        едь

    $ContinueVerbs:
        %lemma
        продолжить
        возобновить
