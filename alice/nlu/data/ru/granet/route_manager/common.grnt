import:
    common/commands.grnt

$Ready:
    %lemma
    готов
    пора
    в салоне
    в $Car

$Car:
    %lemma
    автомобиль
    машина
    такси
    транспорт

$Ride:
    %lemma
    поездка
    движение
    маршрут
    путешествие
    путь
    езда
    ход

$Place:
    %lemma
    место
    конечная? точка
    пункт назначения?
    адрес

$PlaceDescription:
    %lemma
    [(выбранный|заданный|назначенный|указанный|намеченный) (заранее|ранее)?]
    конечный
    %force_negative
    %exact
    выбрать|выбери
    задать|задай
    назначить|назначай|назначь
    указать|укажи
    наметить|наметь


$RideDescription:
    %lemma
    [(выбранный|заданный|назначенный|указанный|построенный|намеченный|проложенный) (заранее|ранее)?]
    текущий
    настоящий
    беспилотный
    
    %force_negative
    %exact
    выбрать|выбери
    задать|задай
    назначить|назначай|назначь
    указать|укажи
    построить|построй
    наметить|наметь
    проложить|проложи

$StartCommands:
        $Inner:
            ехать|едем|едь|езжай
            поехать|поехали|поедем|поезжай
            вперед
            погнали
            трогать|трогаем|трогаться|трогаемся|трогай|трогайся
            выдвигаться|выдвигаемся|выдвигайся
            выезжать|выезжаем|выезжай
            следовать|следуй|следуем
            мчать|мчаться|помчаться|помчимся|помчались
            двигать|двигаться|двигай|двигайся|движемся|двигаем
            приступать|приступи|приступай|приступим
            отправляться|отправляемся|отправляйся|отправимся
            отвезти|отвези|отвози
            гнать|гони
            прокатить|прокати|прокатимся|прокатиться
            управлять|управляй
            старт|стартуй|стартуем|стартанем
            начало|начать|начинай|начни|начинаем|начнем|начинать|начинаю|начните|начинайте
            приступить|приступим|приступаем
            жми (на газ)?
        [(прошу|можно)? $Inner+ (к $Ride)?]

$SingleStartCommands:
    едем|едь|езжай
    поехали|поедем|поезжай
    вперед
    погнали
    трогаем|трогаемся|трогай|трогайся
    выдвигаемся|выдвигайся
    выезжаем|выезжай
    помчимся|помчались
    двигай|двигайся|движемся|двигаем
    отправляемся|отправляйся|отправимся
    гони
    прокати|прокатимся
    старт|стартуй|стартуем|стартанем
    жми (на газ)?

$StopAdjectives:
    %lemma
    временный|временная
    короткий
    ближайший
    разрешенный
    промежуточный
    непродолжительный
    небольшая
    возможное
    где можно остановиться

$StopNouns:
    %lemma
    [$StopAdjectives* остановка ((в|по) $Ride)*]
    конец $Ride

$StopAmbiguous:
    %lemma
    остановить|остановиться

$StopRideVerbs:
    тормози
    тормозни
    притормози
    %lemma
    $StopAmbiguous
    высадить
    припарковаться|парковаться
    приостановиться

$StopVerbs:
    %lemma
    $Common.Stop
    $Common.ToStop
    приостановить
    остановиться|остановить
    останавливаться
    прерви
    $StopRideVerbs

$Route:
    $Inner:
        %lemma
        путь следования?
        маршрут
        дорога
        схема
        направление
        движение
        поездка
        траектория
        (план|детали) поездки
        навигация
        передвижение
    [$RideDescription* $Common.PronounMe* $Inner+]
