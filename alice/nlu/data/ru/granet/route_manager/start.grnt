import:
    common/filler.grnt
    route_manager/common.grnt


form alice.route_manager.start:
    root:
        $RouteStart
    filler:
    
        $Common.WayToCallAlice
        $Common.Filler
        %force_negative
        ты
        мне
        гугл
        яндекс

$RouteStart:
    $LeftFillers* [$ImReady? $Common.Lets? $StartCommands+ (меня|нас)? $Car* самостоятельно? (по|к|в)* $RideDescription* ($Ride|$Place)+] 
    $LeftFillers* [$ImReady? $Common.Lets? $StartCommands+ (меня|нас)? $Car* самостоятельно? (по|к|в)* $RideDescription* ($Ride|$Place)*] $RightFillers*
    $LeftFillers* [$ImReady? $Carry+ (меня|нас)]
    $LeftFillers* $Phrases+
    $LeftFillers* $TurnOn $RideDescription* $Ride

    $Phrases:
        в путь дорогу?
        отдать швартовый
    
    $ImReady:
        [(я|мы|все|пассажир|пассажиры)* $Ready+]
        (я|мы|все|пассажир|пассажиры) (сел|села|сели)

    $RightFillers:
        %lemma
        в путь
        с богом
        с ветерком
        с места
        давай
        быстрее
    
    $LeftFillers:
        яндекс
        все (в сборе)?
        ну что ж?
        (заводи|включай|включи) мотор
        не стой
    
    $TurnOn:
        %lemma
        запустить

    $Carry:
        везти|вези
        отвезти|отвези|отвози
    
    $Negatives:
        .* гонишь .*
        .* мне пора .*
        %lemma
        .* отмена .*
        .* песню .*
        .* запусти навык .*
        .* перемотай .*
        .* с начала .*
        .* в начало .*
    
    %force_negative
    $Negatives


