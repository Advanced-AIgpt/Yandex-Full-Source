import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt

form alice.random_number:
    slots:
        lower_bound:
            type: sys.num
            source: $LowerBound
        upper_bound:
            type: sys.num
            source: $UpperBound
    root:
        $Common.LeftFiller* $GiveMeNumber
        $Common.LeftFiller* [$GiveMeNumber? $FullRange]
        $Common.LeftFiller* [$GiveMeNumber $PartialRange]

$FullRange:
    от $LowerBound до $UpperBound
    между $LowerBound и $UpperBound
$PartialRange:
    до $UpperBound
    больше $LowerBound
    меньше $UpperBound
$LowerBound:
    $sys.num
$UpperBound:
    $sys.num
$GiveMeNumber:
    $Number
    [$Again+ $Number]
    [$Give+ $Common.PronounMe* $Again* $Number]
$Give:
    $Common.Lets
    %lemma
    выбрать | выдать | назвать | сказать | загадать
$Again:
    еще одно?
    еще один? (раз|разок)?
$Number:
    одно? $Any? число
$Any:
    какое-нибудь | случайное | любое | произвольное | рандомное
filler:
    $Common.Filler
    $Common.WayToCallAlice
    $Common.PronounMe
    сейчас | короче | быстро | теперь
