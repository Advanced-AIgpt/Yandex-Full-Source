import: timer/timers.grnt
import: common/filler.grnt

form personal_assistant.scenarios.timer_how_long:
    root:
        [через сколько $Time? $Timer?]
        [сколько по? $Time? (еще|сейчас)? осталось]
        [сколько $Time? (осталось|остался) (по|в|на|до)? $Cancel? $Timer]
        (алиса ты поставила $Timer сколько осталось там времени)
        (когда ты начнешь звенеть $Timer)
        [через сколько $Time? по? $Timer $Cancel]
        [когда $Cancel $Timer]
        [сколько $Time? осталось? (до (того как)? $Cancel)? ((по|до|на|у|от)? $Timer)]
        [сколько еще осталось? (по|до|на|у)? $Cancel* $Timer+]
        [сколько $Time? прошло ((по|до|на|у|от)? $Timer)]
        [сколько $Time прошло]
        [$Time до $Cancel $Timer]
        сколько осталось до звонка
        сколько осталось до конца
        до конца таймера
        $Time (по|до) $Timer
        через сколько будет таймер
        остаток $Timer


        %negative
        сколько
        [сколько ($Time|$Timer)]

    filler:
        $nonsense
        $Common.Filler
        $Common.WayToCallAlice
        $Common.LeftFiller
        у? $Common.PronounMe
        у? $Common.PronounYou
        того как
        там


    $Time:
        %lemma
        времени|минут|секунд

    $Timer:
        %lemma
        таймер

    $Cancel:
        %lemma
        закончится
        врубится
        сработает
        истечет
        запикает
        зазвенишь
        зазвонишь
        включить
        прозвенит
        прозвонит
        звонок
        окончание
        конца
        прозвучит
        пройдет
        истечение
        включения
        срабатывания
        завершения
