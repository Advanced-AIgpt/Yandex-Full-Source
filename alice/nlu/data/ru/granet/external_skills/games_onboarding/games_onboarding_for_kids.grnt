import: external_skills/games_onboarding/common.grnt
import: common/commands.grnt
import: common/verbs.grnt

form alice.external_skill_games_onboarding.kids:
    slots:
        gender:
            source: $ChildGender
            type: ChildGender
        for_age:
            source: $ForAgeNum
            type: sys.num
        before_age:
            source: $BeforeAgeNum
            type: sys.num
    root:
        $ChildGames
        $LetsPlayChildGames
        $WhatChildGamesExists

$LetsPlayChildGames:
    [($Common.Lets? $Play|$Show) $ChildGames $How?]
    [($Common.Lets? $Play|$Show) $For? $Child $Games]
    [($Common.Lets? $Play|$Show) $Games $WithParams]

$WhatChildGamesExists:
    [$What $ChildGames $Exists]

$How:
    быстро
    бесплатно

$What:
    какие

$Exists:
    есть
    существуют
    знаешь
    бывают

$ChildGames:
    [$WhatKindOfGame? $Child? $Games $WithParams]
    [$WhatKindOfGame? $For? $Child $Games]

$WithParams:
    $ForGender
    $WithAge
    $ForGenderWithAge

$ForGenderWithAge:
    $For $ChildGender $ForAge

$ForGender:
    $For $ChildGender

$WithAge:
    [$For $Child? $ForAge]
    [$For $Child? $BeforeAge]

$ForAge:
    $ForAgeNum $Years?

$BeforeAge:
    до $BeforeAgeNum? $Years

$Years:
    лет
    годиков

$ForAgeNum:
    $sys.num
    $ChildAge
$BeforeAgeNum:
    $sys.num

entity ChildAge:
    lemma: true
    values:
        3:
            трехлетний
        4:
            четырехлетний
        5:
            пятилетний
        6:
            шестилетний
        7:
            первоклашка
            первоклассник
        8:
            второклассник
            второклашка
        9:
            третьеклашка
            третьекласник

entity ChildGender:
    lemma: true
    values:
        boy:
            мальчик
        girl:
            девочка

$Play:
    играть
    сыграем
    поиграем
    $Common.ToWant (поиграть|играть)

$Child:
    %lemma
    детская
    детей
    ребенок

$WhatKindOfGame:
    %lemma
    бесплатная
    детские
    нормальные
    интересные
    мини

$Games:
    %lemma
    игра
    развлечение
    занятие

$For:
    для
    мне

$Show:
    %lemma
    показать
    включи
    дай
    нужны
    найди

filler:
    $Onboarding.Filler
