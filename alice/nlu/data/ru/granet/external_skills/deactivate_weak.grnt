import: common/filler.grnt
import: external_skills/common.grnt

form alice.external_skill_deactivate_weak.ifexp.bg_exp_enable_skill_weak_deactivate_new:
    root:
        $Root+

    $Root:
        $Rude
        $WhatTimeIsIt
        $Stop+ ($GameOrPlay | загадок | загадки)?
        $StopGame
        $MusicOrRadio
        $ImTired
        $GoodNight
        $Weather
        $LaunchSomething
        $WhatGamesCanYouPlay
        $LetsTalk
        [$AnotherGame $Launch? $DoYouHave?]

    $Rude:
        сука
        иди (на хуй | в жопу)
        ты (тупая | глупая)

    $LaunchSomething:
        $Launch .+

    $Launch:
        %lemma
        запусти
        включи

    $DoYouHave:
        [(у тебя)? есть]

    $WhatTimeIsIt:
        сколько сейчас? времени
        сколько время
        который час

    $Stop:
        %lemma
        выключить
        выключись
        отключись
        вырубись
        выйти
        прекратить
        остановись
        останови
        стоп
        замолчи
        заткнись
        хватит
        все
        отстань
        отмена
        закончим
        завершить
        перестань
        перезагрузись
        конец
        усни
        спи
        спать
        умри

    $GameOrPlay:
        %lemma
        игра
        навык
        играть
        поиграть
        сыграть

    $Want:
        хочу
        хотим
        буду
        будем

    $StopGame:
        %lemma
        [$Stop $GameOrPlay] .*
        не будем играть .*
        [больше? (не $Want?)] $GameOrPlay .*
        надоело $GameOrPlay

    $Another:
        %lemma
        другую
        что-нибудь другое
        что нибудь другое
        какую-нибудь другую
        какую нибудь другую

    $AnotherGame:
        [$Another $GameOrPlay+]

    $TurnOn:
        включи
        вруби
        поставь

    $MusicAdjective:
        %lemma
        детские
        спокойную
        веселую

    $MusicContentType:
        %lemma
        музыка
        музон
        радио
        песня
        трек

    $MusicOrRadio:
        $TurnOn? $MusicAdjective? $MusicContentType .*
        $Stop $MusicContentType

    $GoodNight:
        спокойной ночи

    $ImTired:
        %lemma
        (я | мы)? устал
        (мне)? (надоело | скучно)

    $WeatherModifier:
        какая
        завтра
        сейчас
        будет
        сегодня

    $Weather:
        $WeatherModifier* погода .*
        (расскажи|скажи) погоду .*

    $LetsTalk:
        [(поговорим | побеседуем | поболтаем | обсудим) (с тобой)?] .*

    filler:
        $Common.WayToCallAlice
        $Common.Filler
        $Common.LeftFiller
        в
        во
        я
        мы
        как
        из
