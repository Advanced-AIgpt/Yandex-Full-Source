import: external_skills/common.grnt

form alice.external_skill.flash_briefing.send_details_link:
    root:
        [$Send $Details $ToRead? $OnPhone? $CantFind?]
        [$Send $Details? $ToRead? $OnPhone $CantFind?]
    $Send:
        %lemma
        скинь
        перешли
        отправь
        вышли
        открой
        кинь
    $Details:
        $DetailedLink
        $DetailsSome
    $DetailedLink:
        $Detailed? $Link $OnText?
    $Link:
        %lemma
        ссылка
        ссылочка
    $DetailsSome:
        %lemma
        подробности
        детали
    $Detailed:
        %lemma
        подробная
    $OnText:
        на $Text
    $Text:
        новость
        текст
    $OnPhone:
        на $Phone
    $Phone:
        %lemma
        телефон
        мобила
    $ToRead:
        %lemma
        почитать
    $CantFind:
        %lemma
        не могу найти
filler:
    здесь
    мне
    ее
    ну
    давай
    больше
    $nonsense
    $Skills.Filler

