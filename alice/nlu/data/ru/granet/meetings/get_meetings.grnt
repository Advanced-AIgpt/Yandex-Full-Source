import: common/commands.grnt
import: common/filler.grnt
import: common/prepositions.grnt
import: common/questions.grnt
import: common/surfaces.grnt
import: common/tense.grnt
import: common/questions.grnt

$Show:
    %lemma
    $Common.Search
    $Common.Speak
    $Common.Tell
    $Common.Ask
    показать
    покажи
    информация
    не? подскажи
    продиктуй
    прочитай
    скажи
    давай посмотрим на?
    расскажи про?
    глянь
    где
    проверь
    посмотреть
    просмотреть
    открой
    перечисли
    назови
    расскажи
    найти
    озвучь
    напиши
    необходима
    предоставь
    записаны
    %negative
    $Common.Set
    %lemma off
    запиши
    заведи
    завести
    назначь
    назначить

$QuestionWhat:
    %lemma
    сколько
    с кем
    какая
    $Common.QuestionWhat

$Planned:
    %lemma
    (по|в)? заметках
    (по|в)? плану
    (по|в)? расписанию
    будут
    запланированы
    напланированы
    на? (ожидаем|ожидаются|ожидаю) время?
    на? (планирую|планируются) время?
    расписание
    расписаны

$Remind:
    %lemma
    напомни

$My:
    %lemma
    у? $Common.PronounMe

$CalendarDate:
    $sys.date
    %value "today"
    сегодняшней | сегодняшние | сегодняшних | этот день | (на|в) эту дату | сегодня
    %value "tomorrow"
    завтрашней | завтрашние | завтрашних | следующий день

$When:
    %lemma
    ($Common.At)? эту? $CalendarDate (число|дату|день)?

$QuestionWhere:
    %lemma
    и? где

$QuestionWhen:
    %lemma
    на? когда
    время
    со скольки
    ($Common.At|$Common.From) (сколько|какое) время?
    ($Common.At|$Common.From) (сколько|какое)? время

$MeetingWordBase:
    встреча
    совещание

$MeetingWordSg:
    $MeetingWordBase<g:sg,nom|sg,gen|sg,dat|sg,acc|sg,ins|sg,abl|sg,voc>
    %negative
    встречи

$MeetingWordPl:
    $MeetingWordBase<g:pl,nom|pl,gen|pl,dat|pl,acc|pl,ins|pl,abl|pl,voc>
    встречи

$MeetingWord:
    $MeetingWordSg
    $MeetingWordPl

$NextMarker:
    %lemma
    будущая
    грядущая
    (на|в)? ближайшая
    в скором времени
    последующая
    предстоят
    предстоящая
    следующая
    дальше
    еще (какие (то|нибудь)?)?
    (на|в)? предстоящее время?

$MeetingNumber:
    $sys.num

$LastMarker:
    %lemma
    заканчиваются
    завершающая
    последняя

$CurrentMarker:
    %lemma
    текущая

$QueryTarget:
    %lemma
    $MeetingNumber
    $NextMarker
    $LastMarker
    $CurrentMarker
    намеченная
    начинаются
    после этой?
    (в|с|по)? утра
    на? день
    на неделю

$Query:
    %lemma
    (кто|какая)? $QueryTarget

$MeetingDescription:
    %lemma
    рабочие
    деловые
    личные
    персональные
    по работе
    с (начальником|коллегами)

$Meeting:
    %lemma
    ($Common.About|$Common.At|$Common.From|$Common.With|$Common.To)? [$Query? $MeetingDescription? $My? $MeetingWord]

$CalendarWord:
    %lemma
    календарь
    расписание
    записи

$Calendar:
    %lemma
    [моем? персональном? личном?] $CalendarWord

$InCalendar:
    %lemma
    (в|на|по|из|с)? $Calendar

$GetMeetings:
    [$Show+ $QuestionWhen* $When* $QuestionWhere* $QuestionWhat* $My* $Query? $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen+ $When* $QuestionWhere* $QuestionWhat* $My* $Query? $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen* $When+ $QuestionWhere* $QuestionWhat* $My* $Query? $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen* $When* $QuestionWhere+ $QuestionWhat* $My* $Query? $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen* $When* $QuestionWhere* $QuestionWhat+ $My* $Query? $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen* $When* $QuestionWhere* $QuestionWhat* $My* $Query $Meeting+ $Planned* $InCalendar*]
    [$Show* $QuestionWhen* $When* $QuestionWhere* $QuestionWhat* $My* $Query? $Meeting+ $Planned+ $InCalendar*]
    [$Show* $QuestionWhen* $When* $QuestionWhere* $QuestionWhat* $My* $Query? $Meeting+ $Planned* $InCalendar+]
    $Meeting

$GetMeetingsFiller:
    %lemma
    (какие|кем) (либо|нибудь|то)
    (нужно|можно)
    (эти|этот|те|там)
    будет
    время
    график
    давай
    до (обеда|ужина|завтрака|ланча)
    есть ли?
    есть ли?
    еще
    идет
    или нет
    ли
    о
    план
    по порядку
    порядок
    с кем
    с? утра
    самая
    список
    уточнить
    хочу все? (знать|узнать)
    %lemma off
    (назначены|назначена|назначили|назначеные|назначенные)

$Goodbye:
    .* (до .? встречи) .*
    %negative
    .* $QuestionWhen .*
    .* $QuestionWhere .*
    .* $QuestionWhat .*

$HowToSetMeetingUp:
    .* назначить .*
    .* $Common.Set .*

form alice.get_meetings:
    slots:
        singular_marker:
            type: string
            source: $MeetingWordSg
        plural_marker:
            type: string
            source: $MeetingWordPl
        next_marker:
            type: string
            source: $NextMarker
        meeting_number:
            type: int
            source: $MeetingNumber
        last_marker:
            type: string
            source: $LastMarker
        calendar_date:
            type: sys.date
            source: $CalendarDate

    root:
        %lemma
        $GetMeetings
        $Remind ($QuestionWhen|$QuestionWhat) $GetMeetings
        ($QuestionWhen|$QuestionWhat) $GetMeetings $Remind
        %negative
        $Goodbye
        %force_negative
        $HowToSetMeetingUp

    filler:
        $Common.At
        $Common.Filler
        $Common.WayToCallAlice
        $nonsense
        $Common.SmartSpeaker
        $Common.LeftFiller
        $GetMeetingsFiller
