import: common/adjectives.grnt
import: common/adverbs.grnt
import: common/commands.grnt
import: common/conjunctions.grnt
import: common/filler.grnt
import: common/prepositions.grnt
import: common/pronouns.grnt
import: common/verbs.grnt
import: open_site_or_app/common.grnt

$ReadSiteShortcut:
    %type custom.app_data
    %value {
        "need_feature": "CanReadSites",
        "nav": {
            "intent": "shortcut.read_site",
            "url": {
                "_": "sitereader://read",
                "desktop": "sitereader://read"
            },
            "text": "Хорошо",
            "tts": "Хорошо",
            "voice_name": "чтение страницы",
            "text_name": "чтение страницы",
            "fallback_tts": "Не могу прочитать страницу",
            "fallback_text": "Не могу прочитать страницу"
        }
    }

    %lemma
    [($Read|$WantToListen)+ $ReadObject+ $Filler*]

    $WantToListen:
        %lemma
        [($Common.Lets|$Needed|$Common.ToWant|$Common.Help)+ $Common.PronounsAll* ($Common.Read|$Common.ToListen)+]

    $Read:
        %lemma
        $Common.Read
        воспроизведи
        чтение
        прочтение
        озвучание
        озвучивание
        почитать

    $WrittenDown:
        %lemma
        находится

        %lemma off
        написано
        написан
        написана
        написаны
        выписано
        выписан
        выписана
        выписаны

    $Site:
        %lemma
        страница
        страничка
        сайт

    $Text:
        %lemma
        $Site
        текст
        статья
        документ
        чтиво

    $ReadObject:
        %lemma
        $Text
        [что $Common.Here* $Common.There* $WrittenDown $Common.At* $Common.PronounsAll* $Common.Opened* веб* $TextOrigin*]

    $Display:
        %lemma
        экран
        дисплей
        компьютер

    $TextOrigin:
        %lemma
        $Browser
        $Text
        $Display
        интернет

    $ReaderActionWord:
        %lemma
        $Common.ToOpen
        $Common.ToStudy
        $Common.ToRead
        $Common.ToLook

    $Font:
        %lemma
        шрифт
        текст
        буквы
        символы

    $Reason:
        %lemma
        занят
        [заняты все? руки]
        [не могу? ($Common.ToLook|$Common.ToSee)]
        [$Common.Very* $Common.Small $Font]

    $Filler:
        $Common.Aloud
        $Common.At
        $Common.ConjunctionThat
        $Common.Filler
        $Common.For
        $Common.From
        $Common.Help
        $Common.Here
        $Common.Immediately
        $Common.Lets
        $Common.Opened
        $Common.PronounsAll
        $Common.There
        $Common.ToWant
        $Display
        $Needed
        $ReaderActionWord
        $Reason
        $Text
        $TextOrigin
        $WrittenDown
        %lemma
        данный
        информация
        содержание
        веб
        у
