import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt
import: open_site_or_app/common.grnt

$CleaningShortcut:
    %type custom.app_data
    %value {
        "need_feature": "CanOpenLink",
        "nav": {
            "intent": "shortcut.cleaning",
            "url": {
                   "_": "https://uslugi.yandex.ru/category/hozyajstvo-i-uborka/uborka--3124/?utm_source=alice&utm_medium=searchapp&utm_campaign=cleaning",
                   "desktop": "https://uslugi.yandex.ru/category/hozyajstvo-i-uborka/uborka--3124/?utm_source=alice&utm_medium=searchapp&utm_campaign=cleaning"
               },
           "text": "Всё поняла. Скоро будет чисто. Уборка от 1690 рублей на Яндекс.Услугах",
           "voice_name": "яндекс услуги",
           "text_name": "Яндекс.Услуги"
       }
    }

    [$Lets* $Common.PronounMe* $Want* $CallCleaning+ $Filler*]

$CallCleaning:
    [$Command+ ($Cleaning|$CleanerOrCompany|$YandexServices)+ $AtHome* $INeedHelp* $Filler*]
    [$Find+ $CleanerOrCompany+ $Cleaning* $AtHome* $INeedHelp* $Filler*]

    [$Help+ $Needed* $Common.PronounMe* с? $Cleaning+ $AtHome* $YandexServices*]
    [($Help|$Needed)+ $Common.PronounMe* $ToClean+ $AtHome+ $Filler*]

    [$Common.Open+ $YandexServices+ $Filler*]
    [($Find|$Command)* $Cleaning+ $YandexServices+ $AtHome* $INeedHelp* $Filler*]

$ToClean:
    убрать
    убраться
    прибрать
    прибраться
    навести чистоту
    помыть
    отмыть
    вымыть

$INeedHelp:
    [($Help|$Needed)+ $Common.PronounMe?]

$Order:
    %lemma
    порядок
    порядочек

$Make:
    %lemma
    навести
    сделать
    замутить

$YandexServices:
    %lemma
    (от|с помощью?|на|через|из|в)? яндекс ($Service|$Cleaning)

$Help:
    %lemma
    помоги
    помощь

$Cleaning:
    $Noun:
        %lemma
        уборка
        чистка
        клининг
        мойка окон?
        мытье окон

    $Adj:
        %lemma
        легкой
        генеральной
        хорошей
        поддерживающую
        полная

    $Preposition:
        с
        по
        на

    [$Preposition? $Adj? $Service? $Noun]

$CleanerOrCompany:
    %lemma
    уборщик
    уборщица
    мойщик
    мойщица
    [(специалист|компания|служба) (для|по)? $Cleaning]
    клининговая (компания|служба)
    клининг

$Command:
    %lemma
    организуй
    назначь
    вызови
    закажи
    позови
    (оформи|сделай|размести) заказ

$AtHome:
    $Noun:
        %lemma
        дом
        квартира
        комната
        хата

    $Preposition:
        в
        на
        по
        с

    $Adj:
        $AdjectiveMy
        %lemma
        всего

    [$Adj? $Preposition? $Noun]

$Filler:
    %lemma
    после? ремонта
    грязь
    грязно
    грязный
    чисто
    для
    найди телефон
    генеральная
    чтобы
    $Preposition? $TimeMomentExtended
    $Preposition? $Service
    (на|в|по|с|через)? яндекс
    $Cleaning.Adj
    можно
    окна
