import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt
import: open_site_or_app/common.grnt

$DarkThemeShortcut:
    %type custom.app_data
    %value {
        "need_feature": "SupportsDarkThemeSetting",
        "nav": {
            "intent": "shortcut.dark_theme",
            "url": {
                "_": "opensettings://?screen=theme",
                "desktop": "opensettings://?screen=theme"
            },
           "text": "Настройки темы приложения",
           "voice_name": "настройки темы приложения",
           "text_name": "настройки темы приложения",
           "fallback_tts": "Не могу включить это здесь. Нужна свежая версия приложения Яндекс",
           "fallback_text": "Не могу включить это здесь. Нужна свежая версия приложения Яндекс"
       }
    }

    $Filler:
        $Preposition
        $Common.PronounMe
        $AdjectiveMy
        $Common.PronounYou
        $Want
        $Needed

        %lemma
        чтобы
        цвет
        свет
        опять
        назад
        снова
        обратно

    [$TurnOnOrOff* $CommonSetting+ $ColorAdj+ ($Theme|$Background|$Mode)+ $Device* $Filler*]
    [$TurnOnOrOff+ $CommonSetting* $ColorAdj+ ($Theme|$Background|$Mode)+ $Device* $Filler*]
    [$TurnOnOrOff* $CommonSetting* $ColorAdj+ ($Theme|$Background|$Mode)+ $Device+ $Filler*]

    [($Change|$Return|$Needed)+ $ColorAdj* $Theme+ $Device+ $Filler*]

    [$Common.PronounMe* ($Want|$Needed)+ ($Change|$Return|$TurnOnOrOff)* $ColorAdj+ ($Theme|$Background|$Mode)+ $Device* $Filler*]
    
$Device:
    $Application
    $Phone
    $Browser

$ColorAdj:
    $Color:
        %lemma
        темная
        светлая
        черная
        белая
    
    $Day:
        %lemma
        дневная
        ночная

    $Color
    $Day

$Mode:
    %lemma
    режим

$Theme:
    %lemma
    тема

$TurnOff:
    $Common.TurnOff

    %lemma
    отключи

$TurnOn:
    $Common.TurnOn
    $Common.Open

    попросите # tom requires
    %lemma
    включить
    активировать
    подключить
    установи
    сделай

    
$Change:
    %lemma
    поменять
    изменить
    выбрать
    ставить
    сменить
    сделать
    заменить
    переустановить
    смени
    поменяй
    замени
    переключи

$Return:
    %lemma
    верни

$Background:
    %lemma
    фон
    оформление

$TurnOnOrOff:
    $TurnOff
    $TurnOn
    $Change
    $Return


