import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt
import: open_site_or_app/common.grnt


$OpenPasswordManager:
    $Filler:
        %lemma
        помощник
        джой
        [(в|к|от|на|с) моему? (телефону|смартфону)]
        [(в|из|на|с) яндекс? браузере]
        найди
        ее|его
        скачал
        а? конкретно
        далее
        [чтобы каждый? раз? их? не вводить]
        пусть
        ту|эту|тот|этот

    $Open:
        $Common.Open
        %lemma
        открыть
        включить
        включи
        запустить
        активируй
        используй
        примени
        произведи
        делай

    $Setting:
        $CommonSetting
        %lemma
        [(внести|поправить|изменить) настройки]
        [внести правки]

    $Want_:
        $Want
        нужен

    [$Lets* $Common.PronounMe* $Want_* $Open* $Setting* $Filler* $PasswordManager+]

$PasswordManager:
    %type custom.app_data
    %value {
        "need_feature": "CanOpenPasswordManager",
        "nav": {
            "intent": "shortcut.password_manager",
            "url": {
                "_": "App-prefs:PASSWORDS",
                "desktop": "App-prefs:PASSWORDS"
            },
            "text": "Настройки Менеджера паролей",
            "voice_name": "менеджер паролей",
            "text_name": "менеджер паролей"
        }
    }

    $FillStrict:
        %lemma
        подставляй
        заполняй
        автозаполняет
        введет
        вставляет
        вставит
        предоставляет
        подставит
        перенеси
        перенесет
        перенесется
        переносятся
        подставляются
        вставляются
        вводятся
        заполняются
        автозаполняются
        автоподставляются

    $Fill:
        $FillStrict
        %lemma
        поставь
        напишет
        пишутся
        поставится
        %lemma off
        %negative
        написан
        написано
        поставлен

    $ForFilling:
        ставку  # asr misspell
        %lemma
        автозаполнения
        подставления
        заполнения
        подстановку
        автоподстановка
        автоподставление  # sic!
        ввода
        вставку
        вставление  # sic!
        применение

    $Save:
        сохранен
        добавлен
        записан
        запомнен
        хранятся
        хранится
        %lemma
        сохраняет
        записывает
        запоминает
        сохранить
        сохраняются
        запомнить
        запоминаются
        записываются
        добавь
        добавляет
        хранит
        храни

    $ForSaving:
        %lemma
        сохранения
        хранения
        запоминания
        записи
        добавления

    $Saved:
        %lemma
        сохраненные
        запомненные
        записанные
        добавленные
        хранящийся

    $My:
        мои
        мой
        моих
        моем
        моей
        свой
        свои
        своей
        своем
        своих
        наш
        наши
        нашем
        нашей
        наших

    $TargetApp:
        %lemma
        $Application
        сайта
        соцсети
        соц сети
        социальной сети
        клиента

    $AppSuffix:
        $Filler:
            самому? любимому
        %lemma
        (от|для|к|в|во|на|из|с)? [$My? $Filler? $TargetApp* .]
        %negative
        попроще
        на тебя
        установить
        удалить
        $Common.Open

    $Password:
        %lemma
        [пароль $AppSuffix?]
        %negative
        пароль на (папку|директорию|файл)

    $Login:
        %lemma
        [логин $AppSuffix?]

    $Authorize:
        залогинь
        залогинься
        авторизуй
        авторизируй
        авторизуйся

    $ToLoginActionStrict:
        %lemma
        залогиниться
        залогина
        залогинит
        залогонит  # sic!
        авторизует
        авторизирует  # sic!
        авторизовать
        авторизоваться
        авторизации
        автологин
        зарегистрироваться
        $Authorize

    $ToLoginAction:
        $ToLoginActionStrict
        входа
        войти
        войди
        заходить
        зайти
        зайди

    $Auto:
        %lemma
        автоматически
        автоматом

    $ToLoginStrict:
        [$ToLoginActionStrict $Auto? $Common.PronounMe? $AppSuffix?]

    $ToLogin:
        [$ToLoginAction $Auto? $Common.PronounMe? $AppSuffix?]

    $Sync:
        %lemma
        синкает
        синхронизирует
        синхронизируй

    $ForSync:
        %lemma
        синхронизации

    $Keeper:
        %lemma
        менеджер
        хранилище
        хранитель
        помощник

    $Filler:
        все
        %lemma
        автоматически
        автоматического
        автоматом
        сами
        самостоятельно
        чтобы я смог?
        поможет мне?
        позволит мне?
        для авторизации
        можно
        и
        будет
        в? форму
        (в|к|от|на|во) (этом|всех)? моих? $TargetApp
        далее
        через него?
        умеет
        я? только что
        скачала
        скачанное
        [согласно? учетной записи]
        [скопируй все?]
        новые
        найди
        найти
        за меня
        (под|в) (строку|поле) паролей
        ваше
        (от|со|с) всех? моих? аккаунтов
        (от|со|с) всеми? моими? приложениями
        (чтобы|с|для) было? автозаполнение?
        пожалуйста

    $Preposition:
        в|из|у|от|через

    $PrepositionToLogin:
        для|которая|которое|чтобы|чтоб

    $Settings:
        %lemma
        настрой
        настроить
        настройка
        подключи
        подключить
        подключение

    $With:
        через|с помощью|с|под|подо|со

    %lemma
    $Application? яндекс пароли
    [($Application|$Keeper)+ $Preposition* $Filler* (с|со)* $My* $Saved? ($Password|$Login)+ $ToLogin*]
    [($My|$Saved|$Settings)+ $Password+ ($ToLogin|$Save|$Fill)* $Filler*]
    [$With* ($Saved|$ToLogin)+ $My* $Login+ $Filler*]
    [$Application? (которая|которое|с|где|чтобы|чтоб)* $Filler* ($FillStrict|$Save|$Sync)+ $Fill* $My? $Saved? ($Password|$Login)+ $PrepositionToLogin* $ToLogin*]
    [$Application? (для|по)* $Filler* ($ForFilling|$ForSaving|$ForSync)+ $With* $My* $Saved? ($Password|$Login)+]
    [($Application|$Password)+ $PrepositionToLogin* $Filler* $Common.PronounMe* $ToLoginStrict+ $ToLogin*]
    [$Authorize+ (меня|подо мной)* $With* ($Application|$Keeper)* $Filler* $My* $Saved* ($Password|$Login)* $AppSuffix?]
