import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt

form personal_assistant.scenarios.alarm_what_sound_level_is_set:
    root:
        [$Show? $Which+ $Level? $Volume $IsSet* (у|на|для)? $Alarm $InPercents? $OnTime*]
        [$Show? (на|с) $Which+ $Volume $IsSet* на? $Alarm $InPercents? $OnTime*]
        [(на сколько|как) громко $IsSet* (у|на)? $Alarm $InPercents? $OnTime*]
        [$Show? $Volume (у|на)? $Alarm $InPercents? $OnTime*]
        [у тебя громкий $Alarm]

    filler:
        $nonsense
        $Common.Filler
        $Common.WayToCallAlice
        $Common.LeftFiller
        сейчас
        будет
        моего|твоего|этого
        у $Common.PronounYou|у? $Common.PronounMe
        на завтра|утром|в обед|вечером

    $Show:
        %lemma
        продемонстрируй
        проверь
        покажи
        скажи
        услышать

    $Which:
        %lemma
        какая
        какова
        сколько
        скольки

    $Volume:
        %lemma
        громкость
        уровень громкости?

    $Level:
        максимальная
        минимальная

    $IsSet:
        %lemma
        стоит
        играть
        установить
        сработает
        поставлен
        работает
        заиграет
        звучит
        звонит
        прозвенит
        включится
        включишь
        играется
        воспроизводишь

    $Alarm:
        %lemma
        будильник
        разбудишь
        будешь будить

    $InPercents:
        %lemma
        в процентах

    $OnTime:
        (в|на)? $sys.time|((в|на|во)? ($sys.date|$sys.weekdays))
