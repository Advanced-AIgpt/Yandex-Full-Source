import: common/commands.grnt
import: common/filler.grnt
import: common/nouns.grnt

form personal_assistant.scenarios.alarm_reset_sound:
    root:
        [$Delete ($Common.TheMusic|$Common.TheTrack|$Common.TheSound|$Other<g:acc|gen>) (на|с)? $Alarm+]
        [$Set $Kind+ ($Common.TheMusic|$Common.TheTrack|$Common.TheSound|$Other<g:acc|gen>) на? $Alarm+]
        [$Change (на? $Kind)+ ($Common.TheMusic|$Common.TheTrack|$Common.TheSound|$Other<g:acc|gen>) на? $Alarm+]
        [Верни $Kind+ ($Common.TheMusic|$Common.TheTrack|$Common.TheSound|$Other<g:acc>) на? $Alarm+]
        [(Разбуди меня|Буди меня) $Kind+ ($Common.TheMusic|$Common.TheTrack|$Common.TheSound|$Other<g:ins>)]
        [$Delete .* ((с|со|на) $Alarm+)]
        [$Other на? $Alarm $Set? $Kind+]
        (поставь (обычный|стандартный) звонок на будильник)

    filler:
        $nonsense
        $Common.Filler
        $Common.WayToCallAlice
        $Common.LeftFiller
        та? которая

    $Kind:
        %lemma
        старую
        стандартную
        дефолтную|дефолтной|дефолтным|дефолт
        изначальную
        предыдущую
        прошлую
        (та? которая)? по умолчанию
        (та? которая)? по дефолту
        пиликающий
        обычную
        как раньше было?
        изначально

    $Delete:
        %lemma
        удали
        очисти
        ресет
        возврат
        убери
        сбрось
        выключи
        отмени
        верни

    $Alarm:
        %lemma
        звук? будильник
        звонок
        сигнал
        настройки

    $Other:
        мелодия
        музыка
        звонок
        писк
        пиканье
        пищание
        рингтон

    $Set:
        $Common.Set
        $Common.TurnOn
        %lemma
        (пусть|пускай) будет

    $Change:
        %lemma
        поменяй
        измени
