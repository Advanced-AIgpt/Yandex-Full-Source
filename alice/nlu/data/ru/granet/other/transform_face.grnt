import: common/filler.grnt
form alice.transform_face:
    slots:
        transform_type:
            type:
                custom.transform_type
                string
            source: $TransformType
    root:
        алиса? $Prefix* $TransformType

# common requests
$Style:
    %lemma
    стиль | фильтр

$Me:
    %lemma
    меня | (мое? фото) | (моя? фотография) | (моя? фотка) | (мои? черты) | (моя? внешность)

$ApplyStyle:
    %lemma
    (примени | сделай) к $Me $Style
    примени $Style
    [как я выглядел если был]
    сделай из меня
    $Style
    я? хочу $Style
    я? хочу быть
    я? хочу стать
    я? хочу (увидеть | посмотреть) (какой я буду? | себя)
    преврати меня в
    покажи? какой я буду?
    покажи меня в $Style
    стилизуй $Me под
    сделай меня? (похожим на)?
    как я буду выглядеть
    какой я буду?
    как стать
    включи как сделать

# target styles
$Male:
    %lemma
    мужик | мужчина | юноша | мальчик | мужественнее | джентельмен

$Female:
    %lemma
    женщина | девушка | девочка | леди | женственнее | девчонка

$Anime:
    %lemma
    (герой | персонаж)? (мультик | мультфильм)? (аниме | диснея)
    (герой | персонаж) (мультик | мультфильм)
    мультяшка

$Orc:
    %lemma
    орк | гоблин | тролль | монстр | чудовище | чудище | огр

$Elf:
    %lemma
    эльф | эльфийка | фея

$Vampire:
    %lemma
    вампир | вурдалак | (граф? дракула) | носферату

$Old:
    %lemma
    (под? старость) | пожилой | бабушка | дедушка | старуха | старик | старый | пенсионер | (на пенсии) | дед | бабка | (в старости) | будущее | (в будущем)

$Young:
    %lemma
    (в (молодости | детстве | юности)) | молодой | юный | ребенок | моложе | младше

$Years:
    %lemma
    десять
    двадцать
    тридцать
    сорок
    пятьдесят
    сто
    тысяча
    миллион

$TransformType:
    %lemma

    %type "custom.transform_type"

    %value "any"
    (измени | стилизуй | замени | поменяй | преврати) $Me
    [измени мою? внешность]
    измени мое лицо на фотографии
    (примени | наложи) маску
    обработай эту фотку
    смонтируй мою фотографию
    обработка фото
    включи маски
    дай мне маску
    хочу измениться
    превращение
    запусти (превращение | изменение внешности)
    включи (превращение | изменение внешности)

    %value "aging"
    [состарь меня]
    [покажи меня в старости]
    [покажи? каким я буду в старости]
    [покажи? каким я буду когда состарюсь]
    [покажи? как я буду выглядеть в старости]
    (измени | поменяй) мой возраст ((по | на) (фото | фотографии))?
    добавь мне $Years лет
    покажи каким я буду через $Years лет
    добавь мне возраст
    $ApplyStyle $Old
    покажи меня в будущем
    телепортни меня в будущее
    кто я буду в будущем
    когда я буду взрослым я какой буду
    какой я буду $Old когда вырасту
    я хочу быть взрослой
    как я (буду | стану) выглядеть? когда вырасту
    кем я (буду | стану) в будущем
    что ждет меня в будущем когда я вырасту
    кто я буду в будущем
    посмотреть будущее
    я в $Old

    %value "rejuvenation"
    [омолоди меня]
    [покажи меня в молодости]
    [покажи? как я выглядел в молодости]
    покажи каким я был $Years лет назад
    $ApplyStyle $Young
    верни мою молодость
    назад в прошлое
    снова (20 | 19 | 18 | 17 | 16)
    душа мне постоянно молодая
    я в прошлом
    увидеть себя из прошлого
    фотку в тиндер
    где мои 17 лет

    %value "male"
    $ApplyStyle $Male

    %value "female"
    $ApplyStyle $Female
    пункты для изменения внешности в девушку soft girl

    %value "anime"
    $ApplyStyle $Anime
    хочу жить в мультике
    сделай меня героем в мультике

    %value "vampire"
    $ApplyStyle $Vampire
    как выглядит $Vampire
    как сделать клыки $Vampire
    как стать $Vampire
    а ты знаешь что я $Vampire
    да я тебя сейчас съем я $Vampire
    мне надо стать $Vampire
    я серьезно $Vampire
    я $Vampire я его просто сожру

    %value "orc"
    $ApplyStyle $Orc
    за орду
    я еще превращусь в халка и тебя в лепешку превращу

    %value "elf"
    $ApplyStyle $Elf
    как выглядит $Elf
    как стать $Elf в реальной жизни
    а? я стану $Elf
    как стать $Elf

$Prefix:
    $Common.LeftFiller
filler:
    $nonsense
    $Common.Filler
    сейчас | короче | быстро | теперь
