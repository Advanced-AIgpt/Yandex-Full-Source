import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt

form alice.game_suggest:
    filler:
        $Common.Filler
        $Common.LeftFiller
        # $nonsense is skipped intentionally right now, add it to improve recall
    root:
        посоветуй в какую игру мне поиграть

form alice.game_suggest.decline:
    filler:
        $Fillers
    root:
        $Decline+
        %negative
        [ты $Already? $Played]

form alice.game_suggest.confirm:
    filler:
        $Fillers
    root:
        $ConfirmPlay+

$Fillers:
    $Common.Filler
    $Common.LeftFiller
    $nonsense
    %lemma
    говорю
    сказал
    думаю
    пожалуй
    слушай
    короче
    определенно
    вовсе
    совсем
    почему то
    как то

$Decline:
    не? пока? [$DontWant $This? $Game?]
    но? $SuggestOther
    но? $DontLike
    но? $AlreadyPlayed
    но? $DontLike
    но? $ForbiddenForMe
    $SomethingElse

$DontWant:
    нет
    не надо
    [я? (не хочу)]
    [мы? (не хотим)]
    [я? (не буду)]
    [мы? (не будем)]
    [мне? (не хочется)]
    неохота
    [я? (не согласен)]
    [я? (не согласна)]
    [мы? (не согласны)]

$SuggestOther:
    [$CanYou? $Show? $Common.PronounMe? $Something? $OtherGame]
    [$DoYouHave $Something? $OtherGame]
    [$Common.PronounMe? $Common.Lets $Something? $OtherGame]
    [$Common.PronounMe? $Asked $Something? $OtherGame]

$AlreadyPlayed:
    [$Common.PronounMe? $Already? $Played]

$DontLike:
    [$Common.PronounMe? (не ($Like|$Liked)) $This? $Game?]

$ForbiddenForMe:
    [$Common.PronounMe? $Forbidden играть (в? $This)? $Game?]

$CanYou:
    [ты? (можешь|могла бы)]
    [давай ты?]

$DoYouHave:
    [(у тебя)? есть]

$Asked:
    [тебя? (просил|попросил)]

$Show:
    поиграем в
    %lemma
    найди
    поищи
    покажи
    порекомендуй
    посоветуй
    знаешь
    включи
    играть в?
    запусти

$Something:
    %lemma
    еще
    что-то
    какой-то
    какой-нибудь
    что то
    какой то
    какой нибудь
    что-нибудь
    что нибудь

$SomethingElse:
    еще
    что-нибудь еще
    есть что-то еще

$Forbidden:
    нельзя
    запрещено
    не могу
    не умею

$Like:
    %lemma
    люблю
    нравится
    понравится

$Liked:
    понравилось
    понравилась
    понравился

$OtherGame:
    $Other $Game?
    новая $Game

$Other:
    %lemma
    другой
    не этот
    следующий

$Game:
    %lemma
    игра
    игрушка
    вариант

$This:
    %lemma
    этот
    это
    его
    такой
    он
    она

$Played:
    играл
    играла
    играли

$Already:
    %lemma
    уже
    когда-то
    когда то

$ConfirmPlay:
    $Confirm в? $Game?

$Confirm:
    $Common.Agree
    %lemma
    играть
    поиграем
    включить
    запускай
