import:
    common/commands.grnt
    common/filler.grnt
    common/quit.grnt
    suggesters/movie_akinator.grnt

form alice.video_rater.init:
    root:
        хочу? $Rate $Video
    filler: $Filler

form alice.video_rater.rate:
    root:
        $RatingSlotExtended
    slots:
        rating:
            type:
                custom.video_rating
                string
            source: $RatingSlot
    filler: $Filler

form alice.video_rater.quit:
    root:
        [$Quit .*]
    filler: $Filler

form alice.video_rater.irrelevant:
    root:
        [$Irrelevant .*]
    filler: $Filler

$Filler:
    $nonsense
    $Common.Filler
    $IDoRate
    $This
    $VideoWide
    %lemma
    отношусь
    я
    он
    тоже

$Video:
    %lemma
    видео
    кино
    фильм

$VideoWide:
    $Video
    $custom.video_film_genre

    %lemma
    сериал
    мультфильм
    мультик
    мульт
    мультсериал

$This:
    %lemma
    этот
    это
    такой

$Rate:
    оценить
    оценивать

$RateWide:
    $Rate
    %lemma
    оценка
    оценивание

$RatingSlotExtended:
    %lemma
    на? $RatingSlot (балл|звезда|звездочка)? (из (5|пяти))?

$RatingSlot:
    %type "custom.video_rating"
    %lemma

    %value "1"
    1
    один
    единица
    единичка
    кол
    $VeryBad+

    %value "2"
    2
    два
    двойка
    двоечка
    $Bad+

    %value "3"
    3
    три
    тройка
    троечка
    $Normal+

    %value "4"
    4
    четыре
    четверка
    $Good+

    %value "5"
    5
    пять
    пятерка
    пятерочка
    пятюня
    $VeryGood+

    %value "0"
    $DidNotWatch

    %value "-1"
    $Skip

    %value "-2"
    $Undefined

$Skip:
    %lemma
    дальше
    никак
    [ничего не думаю]
    пропусти
    пусти
    следующий

$Quit:
    $Common.BackOff
    $Common.Cancel
    $Common.Exit
    $Common.IAmDone
    $Common.Stop
    $Common.TurnOff
    %lemma
    бесить
    закрой ебало
    конец
    на главную
    назад
    на начальный экран
    на хуй
    не (буду|хочу)? оценивать
    не предлагать
    [не сходи с ума]
    не хотеть с тобой разговаривать
    не хочу
    перезагрузись
    перезагрузить
    пока
    раздражать
    сбрось
    тупая
    ты что совсем ку ку

$Irrelevant:
    включи
    найди
    покажи
    запусти
    поставь
    посмотреть
    погода
    время

$IDoRate:
    оцениваю
    ставлю

$VeryBad:
    %lemma
    dislike
    гадость
    говно
    дерьмо
    дизлайк
    дно
    дрянь
    еще хуже
    ноль
    отвратительно
    отвратительный
    отстой
    отстойно
    отстойный
    оценка 1
    [очень плохо]
    [очень плохой]
    параша
    [полное говно]
    ужас
    ужасно
    ужасный
    фигня
    фиговый
    фу
    херня
    херово
    херовый
    хреново
    хреновый
    хрень
    хуево
    хуевый
    хуйня

$Bad:
    %lemma
    [мне он не нравится]
    ерунда
    не интересно
    не люблю
    не нравится
    не особо
    не очень (подходит)?
    не понравился
    не рекомендую
    нет
    нет не нравится
    нет не очень
    ни о чем
    ну так
    ну такое
    оценка 2
    плохой
    плохо
    скучный
    скучно
    слабый
    слабо
    так себе

$Normal:
    %lemma
    банальный
    банально
    на один вечер
    ничего
    норм
    нормалек
    нормально
    нормальный
    нормик
    нормуль
    обычно
    обычный
    ок
    пойдет
    понравился
    пять из десяти
    смотрела нормально
    сойдет
    средне
    средненький
    средний
    удовлетворительно
    удовлетворительный
    можно посмотреть

$Good:
    %lemma
    good
    веселый
    весело
    весьма неплохо
    гуд
    гут
    да
    лайк
    на четверочку
    неплохо
    неплохой
    нравится
    оценка 4
    прикольно
    прикольный
    приятный
    хороший
    хорошо
    четверочка
    смешно
    смешной

$VeryGood:
    %lemma
    like
    божественно
    божественный
    великолепно
    великолепный
    десятка
    десять
    десять из десяти
    заебись
    замечательно
    замечательный
    зашибись
    идеально
    идеальный
    интересно
    интересный
    интересный сюжет
    класс
    классно
    классный
    клево
    клевый
    крутейший финал
    круто
    крутой
    лайк суперлайк
    лучший
    любимый
    [мне он очень нравится]
    мой любимый
    обожаю
    огонь
    отлично
    отличный
    отличный сценарий
    офигенно
    офигенный
    охрененно
    охрененный
    охуенно
    охуенный
    охуительно
    охуительный
    оценка 5
    очень
    [очень классный]
    [очень крутой]
    [очень нравится]
    [очень понравилось]
    [очень смешно]
    [очень смешной]
    [очень хороший]
    [очень хороший добрый]
    [очень хорошо]
    пиздато
    пиздатый
    потрясающе
    потрясающий
    прекрасно
    прекрасный
    супер
    супер лайк
    суперлайк
    суперский
    трогательная
    шедевр
    шикарно
    шикарный
    это мой любимый

$DidNotWatch:
    %lemma
    [не знаю (такой|такого)]
    [не слышал(а|и)? (о таком|про такой)?]
    [не (смотрел|видел)(а|и)? ((такой|этот) $VideoWide)?]
    о чем он

$Undefined:
    %lemma
    не помню
    не знаю
    переоценен
    смотрел
