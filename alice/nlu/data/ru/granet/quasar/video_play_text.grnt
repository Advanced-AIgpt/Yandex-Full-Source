import:
    common/filler.grnt
    common/commands.grnt
    quasar/video_common.grnt

form alice.quasar.video_play_text:
    slots:
        video_action:
            source: $VideoAction
            type: string
        video_type:
            source: $VideoType
            type: string
        video_text:
            source: $VideoText
            type: string
    root:
        $Root

$Root:
    [$VideoAction+ $VideoType? $VideoText $InTheNet?]
    %negative
    $VideoAction .? $NotVideo .*

$Pronoun:
    мне
    нам
    его

$VideoActionList:
    $Common.Play
    $ActionSlot
    $Find
    %lemma
    какие (у тебя)? есть?
    посмотреть
    %negative
    (что | какие) нибудь

$VideoAction:
    $VideoActionList $Pronoun?

$NotVideo:
    будильник
    таймер
    навык
    песня
    песню
    песни
    музыка
    музыку
    плейлист
    радио
    группу
    альбом
    погода
    погоду
    играть
    поиграем
    сыграем

$InTheNet:
    в (сети|интернете)

$VideoGenre:
    $custom.video_film_genre

$VideoType:
    $VideoDict
    $VideoGenre
    %lemma
    фильм
    сериал
    клип
    видео
    видос
    трейлер
    %negative
    серия
    сезон

$VideoText:
    .+
    %negative
    $Garbadge

$BadAction:
    навык|поиграем

$Garbadge:
    алиса
    %lemma
    пожалуйста
    следующий
    онлайн
    в хорошем качестве

$Sources:
    ivi
    иви
    %lemma
    ютуб
    youtube
    кинопоиск
    амедиатека

$Source:
    (в | на)? $Sources

filler:
    $nonsense
    $Common.Filler
    $Garbadge
    $Source
