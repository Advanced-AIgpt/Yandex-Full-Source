import:
    common/filler.grnt
    common/commands.grnt
    quasar/video_common.grnt
    quasar/video_channels_common.grnt

$ShowTvProgramm:
    [$AtCurrentTime? $TotalProgrammActions* $TvGenre+ $ChannelTvItem+ $MediumFiller*] #жанр на тв
    [$AtCurrentTime? $TotalProgrammActions+ $AnyProgramExpression] #любая передача на тв

$TvGenre:
    $VideoDict
    %lemma
    детям
    детская
    детская передача
    детей
    детектив
    кино
    кинчик
    кинишка
    киношка
    кинофильм
    комедия
    концерт
    мультфильм
    мультик
    мульт
    мультсериал
    новости
    сериал
    сериальчик
    спорт
    спортивная
    спортивная передача
    телефильм
    ток шоу
    телесериал
    телесериальчик
    художественный фильм
    художественное кино
    художественный кинофильм
    фильм
    фильмец
    футбол

$AnyNameWord:
    .
    %negative
    %lemma
    $TvChannelOrSomethingElse
    на
    тв
    эфир
    кино
    мой
    центр
    купить
    недорого
    бэушный
    дешевый
    usb
    lg
    браузер
    dvd
    $AnyTimeOrNumber
    $FalseChannelItem
    $StrongFiller
    $ExplicitChannelMarker
    $TvChannelOrSomethingElse
    $ProgrammList

$NotProgramms:
    новый
    (тихий|громкий)? звук
    громкость
    вай фай

$AnyProgramName:
    $AnyNameWord+
    %negative
    $TrueTvItem
    %lemma
    $NotProgramms
    не? $TotalProgrammActions
    $ExplicitChannelMarker
    $GrayProgrammItem 
    $FalseChannelItem
    $TvChannelOrSomethingElse
    $SgTvItem
    $StrongFiller
    $MediumFiller
    $ProgrammList
    [(на|по|$SgTvItem)? $AnyTimeOrNumber $TvChannelOrSomethingElse* $StrongFiller*]
    [(на|по|$SgTvItem)? (следующий | предыдущий | все | список) $ExplicitChannelMarker*]

$ExplicitChannelMarker:
    %lemma
    канал
    телеканал
    кнопка

$AnyProgramExpression:
    [$AnyProgramName ((в|на|по)? $ChannelTvItem)+ $TvGenre?]
    %negative
    [$TvChannelOrSomethingElse $FalseChannelItem? $ExplicitChannelMarker]
    [(в $AnyProgramName) ((в|на|по)? $ChannelTvItem)+ $TvGenre?]
    [(во $AnyProgramName) ((в|на|по)? $ChannelTvItem)+ $TvGenre?]

$ChannelTvItem:
    $TrueTvItem
    %lemma
    $SgTvItem<g:sg>
    %negative
    %lemma off
    $GrayProgrammItem
    $SgTvItem<g:pl>
    [$FalseChannelItem+ $TrueTvItem*]

$TrueTvItem:
    %lemma
    #тв
    телевидение
    телепередача
    прямой? телеэфир
    тиви
    прямая? трансляция
    прямой? эфир
    (по|на|в) тв

$SgTvItem:
    телевизор
    (по|к)? телевизору
    (на|в)? телевизоре
    телек
    (по|к)? телеку
    (на|в)? телеке
    ящик
    (по|к)? ящику
    (на|в)? ящике
    приставка
    (по|на|в|к)? приставке

$GrayProgrammItem:
    %lemma
    передача
    программа
    телепрограмма

$ProgrammList:
    %lemma on
    список
    все
    лучшие
    мне

$FalseChannelItem:
    %lemma
    радиоканал
    радиостанция
    расписание
    станция
    ютуб
    по? ютубу
    (на|в)? ютубе
    сайт
    на? сайте
    интернет
    по? интернету
    (на|в)? интернете
    следующий
    предыдущий
    телефон
    номер телефона
    навык

$OtherProgrammActions:
    %lemma
    быть
    искать
    доступен
    доступный
    найти
    поглядеть
    поискать
    показывать
    посмотреть
    смотреть
    хочу
    %negative
    %lemma off
    покажут
    будет
    показали
    показывали
    было
    смотришь

$TotalProgrammActions:
    $OtherProgrammActions
    $Action
    %negative
    все выпуски
    не $Action
    не $OtherProgrammActions
    как играть
