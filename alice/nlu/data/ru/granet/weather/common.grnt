import: common/commands.grnt
import: common/filler.grnt

$SingleWeatherWords:
    # слишком общие запросы надо уводить из формы - https://st.yandex-team.ru/DIALOG-3565#5c48d9f92f38ef001f5c1ead
    %lemma
    $Weather.ExtraWeatherWord

$HotWords:
    %lemma
    температура
    влажность
    мороз
    давление
    пасмурно
    градусы
    карта осадков

$PrecMap:
    %lemma
    [карта осадков]
    [осадки на карте]
    [карта с (дождем | дождями)]
    [карта погоды]
    [карта дождей]
    [погода на карте]
    [дождевая карта]
    [карта с осадками]
    [погодная карта]

$PrecMapFiller:
    %lemma
    яндекс
    онлайн
    [в реальном времени]
    [в режиме реального времени]
    мне
    нам

$ShowMe:
    %lemma
    $ShowWord:
        %lemma
        показать | покежь | открыть | вывести | отобразить | нарисовать
        дать | давать | выдать | кинуть | кидануть | отправить
        (дать | давать | кинуть | отправить | показать | вывести | кидануть)? (ссылка | ссыль | линка)

    $SeeWord:
        %lemma
        увидеть | посмотреть | глянуть | позырить | почекать
        чекнуть | узреть | взглянуть | попялить | поглазеть
        рассмотреть | смотреть
    $WantWord:
        $Common.Lets
        %lemma
        не отказатся бы? от? | можно и? | позволить
        меня? интересует | интересно [бы? было?]
        позволить | можешь | мочь

    $SeeWord
    $WantWord
    не? $ShowWord
    $WantWord чтобы ты? $ShowWord
    еще
    бы
    на
    (что | че | чё | чо) по?
    для

$Prepositions:
    в | с | у | о | к
    от | до | на | по | со | из | во | за | об | ко | со
    над | под | при | про | без
    ради | близ | подо
    перед | около | через | вдоль
    после | кроме | сквозь | вроде
    вследствие | благодаря | вопреки
    согласно | навстречу

$NoMatchHandcrafted:
    $Media
    $NonWeatherTemperature
    $Betting
    $BloodPressure
    $MiscPressure
    $School
    $Misc
    $MissdirectedQuestions

    $Media:
        %lemma
        песня | песенка | поют | музыка | трек
        музыканты | певцы | актеры
        альбом | группа | клип | видео
        слушать | послушать | поставь
        стих | стихотворение | стихи | строки
        автор | молитва
        подкаст | аудио | аудиокнига | книга | аудиоспектакль | спектакль

        фотография | сфотографировать | рисунок
        картина | рисовать | рисование | снять
        заснять | записать

        фильм | кино | кинофильм
        мультфильм | мультик | мульт | сказка
        серия | сериал | сезон | эпизод
        билет

        # "телеканал дождь", "радио серебряный дождь" и пр.
        канал | телеканал | радио

        # "открой сайт"
        сайт

        # группа "три дня дождя" и пр.
        три дня дождя
        полгода плохая погода

        # фильмы, музыка, отсылки
        унесенные ветром
        ветер крепчает
        восточный ветер
        ты ветер я вода
        ветер с моря дул
        ветер перемен
        ветер моих перемен
        [ветер в голове]
        [попутный ветер]
        смельчак и ветер
        ветер зимаря
        ветер знает
        [нос по ветру]
        ветром стать
        быстрее ветра
        быстрее чем ветер

        %lemma off
        градусы

    $Betting:
        %lemma
        # "прогноз на матч"
        матч | мачт
        ставка
        игра | игровая
        уефа
        лига
        теннис | футбол | хоккей | баскетбол | спорт

    $NonWeatherTemperature:
        %lemma
        # температура солнца или плавления
        [температура солнца]
        [температура (плавления|пайки)]
        температура (для | у)? человека
        [нет температуры]

    $BloodPressure:
        %lemma
        артерий | артериальное | в артериях
        венозное | в венах
        [(поднять|уменьшить) давление]
        [(повысить|повышено|повышенно|повышенное|повышать|повышает|повысилось) $Pressure]
        [(понизить|понижено|пониженно|пониженное|понижать|понижает|понизилось) $Pressure]
        [(нормально|нормальное|нормализовать|нормализует|нормализовалось|в норме|обычное|обычно|стандартное) $Pressure]
        скакать
        мерить
        таблетки
        болезнь | болею | боль

    $MiscPressure:
        %lemma
        # другое давление
        [давление (жидкости|газа|воды)]
        [давление (топлива|шин|датчик)]

    $School:
        %lemma
        загадка
        задача
        треугольник
        угол

    $MissdirectedQuestions:
        %lemma
        # вопросы "не по адресу"
        почему
        зачем
        кто
        рассчитать
        (как | что | чем | каким образом | кого) (сделать | делать | назвать | называется | получается | влиять | найти | сказать | включить | отключить)
        $Prepositions (что | кто)
        (кто | что) (такой | такая | такие | такое | это)
        это (кто | что)
        [что мне? же? (с (этим | ним | ней | таким))? (делать | сделать)]
        что значит
        где
        откуда
        куда
        [(какой | какое) $When? (был | было)? ((день недели?) | месяц | год | сезон | (время года) | праздник | событие | (лунный день))]

    $Misc:
        %lemma
        # разное
        шум | звук

        # хак чтобы не отвечать на "ясно" ("что тебе ясно")
        ясно

        диабет
        тело
        слово | словосочетание
        алкоголь
        коронавирус
        ребенок
        язык | переведи | английский | французский | немецкий | английски | французски | немецки
        нтв | канал
        цена | стоимость
        доллар | рубль
        град
        кафе | ресторан | столовая
        норма
        любить
        нравиться
        ненавидеть
        радовать
        дождь из
        [$sys.fio.name ($sys.fio.surname | блок | мандельштам | пушкин)]
        нектар
        [тает снег]
        астрологический прогноз
        [белый снег]

        %lemma off
        спроси | спросите

$WhenResolver:
    (в|во|на|за|до)? $When

$WhereResolver:
    $Where
    $UnknownWhere

$StrictWhereResolver:
    $StrictWhere
    $StrictUnknownWhere

$DayPartResolver:
    (в|во|на|за|до)? $DayPart

$ExpectedChange:
    $custom.expected_change
    был | было | была

$PrecType:
    $custom.prec_type
    %negative
    %lemma
    солнце
    град

$When:
    $sys.datetime_range
    $sys.datetime
    $OutsideNow

$OutsideNow:
    %type "sys.datetime"
    %lemma

    %value {"seconds":0,"seconds_relative":true}
    в тени
    за бортом
    за окном
    за окошком
    на солнце
    на улице
    на улочке
    не в тени
    снаружи

$Where:
    (в|во|на|про|по)? [$WhereGeoBase+ $UnknownWhereVertex?]

$StrictWhere:
    (в|во|на|про|по)? $WhereGeoBase+

$ExtendedStrictWhere:
    (в|во|на|про|по)? ($WhereGeoBase | $Settlements)+

$DayPart:
    $custom.day_part

$Settlements:
    # очень широкий список населенных пунктов, импортировано из базы погоды
    %lemma
    %include_raw "weather/settlements_with_permutations.txt"

$WhereGeoBase:
    # геобаза
    $ner.GeoAddr.City
    $ner.GeoAddr.City2
    $ner.GeoAddr.Province
    $ner.GeoAddr.Country
    $Country
    $RegionRu
    # $sys.geo
    # $ner.GeoAddr.Address

    %negative
    # хак чтобы убрать города Зима и Папа, и прочие приколы
    %lemma
    папа
    зима | осень | весна | лето

$Place:
    %lemma
    город
    село
    поселок
    поселение
    район
    область
    край
    страна

$UnknownWhereSource:
    .
    . .
    . . .
    . . . .

    %negative
    $nonsense

    [$sys.num+ .*]

    [.* $Common.Filler]
    [.* $Common.LeftFiller]
    [.* $Common.WayToCallAlice]
    [.* $WeatherFiller]
    [.* $NoMatchHandcrafted .*]

    [.* (в|с|со|от|до|то|о|но|на|без|еще|про)]

    [.* $SingleWeatherWords]
    [.* $TellMe]
    [.* $ExpectedChange]
    [.* $PrecType]
    [.* $WhenResolver]
    [.* $DayPartResolver]
    [.* $Weather]
    [.* $How]
    [.* (зонт|зонтик)]
    [.* ($Common.Lets|взять|брать|понадобится|стоит)]
    [.* время]

$UnknownWhereVertex:
    %lemma
    (в|во|на|про)? [$Place? $UnknownWhereSource]  # приоритет на явное обозначение места
    %weight 10
    (в|во|на|про)? [$Place $UnknownWhereSource]+  # приоритет на явное обозначение места

$UnknownWhere:
    $UnknownWhereVertex

$StrictUnknownWhere:
    %lemma
    # строгое явное обозначение места
    # (город|село|поселок|поселение|район|страна) $UnknownWhereSource
    (в|во|на|про)? [$Place $UnknownWhereSource]+  # приоритет на явное обозначение места

$TellMe:
    %lemma

    $TellWord:
        %lemma
        скажи | расскажи | подскажи | говори | посмотри
        скажешь | расскажешь | подскажешь | покажешь | говоришь | посмотришь
        сказать | рассказать | подсказать | говорить | посмотреть
        спрашиваю | спрашиваем | спрошу | базарни | поясни

    (в|по|на)? (улице|улицу)
    не? $TellWord (про | о | обрасскажи | за)
    не? $TellWord $How?
    $TellWord
    [напомни $How]
    $What насчет
    $What будет
    $What там с?
    $What у нас с?
    $What с
    $What в
    $What [(у нас)? по поводу?]
    [как (дела обстоят?)? (с | по)?]
    как (у нас)? с
    у нас
    когда | во сколько | в какое время | скоро ли?
    в какой половине
    не знаешь
    $How в?
    $Weather.HeavyExtraWeatherWord.WeatherAdjective
    где
    про
    еще
    для
    по

    # где | в каком районе | в каких районах
    какова? вероятность

    открой | покажи | покажите
    подробно | подробная | подробное | подробный | подробные | общая | общий
    меня интересует
    [$Common.PronounMe? $Common.Lets узнать?]
    узнать бы?

    $What:
        что
        че
        чё
        чо

$Weather:
    $WeatherAll
    с $WeatherAll
    о $WeatherAll

    прогноз
    прогноз ($WeatherWord<g:gen> | $ExtraWeatherWord<g:gen> | $HeavyExtraWeatherWord<g:gen>)  # прогноз 'чего?' -> прогноз 'погоды'

    что с $WeatherWord<g:ins>  # что с 'чем?' -> что с 'погодой'
    что за $WeatherWord
    с $WeatherWord<g:ins>  # что с 'чем?' -> что с 'погодой'

    какую $WeatherWord обещают  # какую 'чего?' обещают -> какую 'погоду' обещают

    [очень $ExtraWeatherWord+ ли?]

    $WeatherAll:
        $WeatherWord
        $HeavyExtraWeatherWord  # можно сказать только их, в отличие от ExtraWeatherWord

    $WeatherWord:
        # погодные явления
        %lemma
        погода
        погодка
        [температура воздуха]
        [температура погоды]
        [температура на? улице]
        [атмосферное давление]
        [давление воздуха]
        [давление (в миллиметрах ртутного столба)]

    $ExtraWeatherWord:
        # погодные явления
        %lemma
        температура
        (сильный | слабый | небольшой)? снег (с дождем)?
        [(ветер | ветерок) дует?]
        [дождь (с|со)? гроза]
        [дождь (с|со)? снег]
        градусы
        атмосферное
        без осадков
        буран
        ветренно | ветрено | ветреная | ветренная
        влажно
        гроза
        гром
        дождик
        дождичек
        дождливо
        дождь
        дубак
        жара | жарко | жарища | тепло | тепловатенько
        заморозки
        ливень
        метель
        морозно
        облачно (с прояснениями)?
        осадки
        пасмурно
        порывы
        потепление
        похолодает | потеплеет
        снег
        снежок
        солнечно
        холодно | холод | прохладно | прохладненько | холодноватенько

    $HeavyExtraWeatherWord:
        # погодные явления
        %lemma
        [$WeatherAdjective+ $ExtraWeatherWord+]
        [сколько градусов]
        [градусов тепла]

        $WeatherAdjective:
            %lemma
            сильно | сильный | сильная | сильное | сильные
            большой | большая | большое | большие
            слабый | слабая | слабое | слабые
            небольшой | небольшая | небольшое | небольшие
            низкий | пониженный | ниже среднего | нормальный
            средний | выше среднего | повышенный | высокий

        # социальное взаимодействие
        [(как (одеться|одеваться)|что (одеть|надеть)) если (поехать|ехать|выехать|выйти|пойти|пройтись)]
        [надо? ли? тепло (одеться|одеваться)]
        [можно ли? одеться легко]
        [что? можно? (одеть|надеть|одевать|надевать)? ((в|на)? такую)? погоду]
        # [((в|во) что) (одеть|одевать|одеться|одеваться)]

        (скорость|направление|сила) ветра
        [(ветер | ветерок)+ $How* $WeatherAdjective* дует*]

        %negative
        ветер | ветерок

$How:
    %lemma
    как | кака | какая | сколько единиц? | во сколько | до скольки | со скольки | насколько

$WeatherFiller:
    # специфичные погодные филлеры
    $OutsideNow

    %lemma
    (в|по|на)? городе
    в? течение  # какая погода в течение следующей недели
    воды | (в|на)? море
    по цельсию | в цельсиях | по фарейгейту | в фаренгейтах
    синоптик
    можно
    (в|на) (ближайшее|близкое) время
    (в|на) ближайшие дни
    (в|на) ближайшие часы
    (в|на) ближайший час
    (в|на)? ближайший
    (в|на) (котором|который) (час|часу)
    там | тут | здесь  # TODO(sparkle): нужно ли это удалить?
    по [моему? местоположению]
    на карте | карта
    на после
    долго  # "дождь долго будет?"
    (в|на)? (этот|это)  # "на этот уикенд" -> "уикенд"
    по часам | по времени
    в данный момент
    на всю | на весь | на целый
    до конца
    предстоящую
    табло
    хорошая | или | плохая  # хорошая погода будет или плохая?
    сильная | слабая
    наступит  # когда будет хорошая погода
    точная | точный | точное | точные  # точный прогноз
    включительно  # встречается в промежутках времён
    какой сила  # "ветер какой силы"
    метров в секунду  # "сколько завтра метров в секунду ветер"
    стихнет  # "когда ветер стихнет"
    столбик  # "атмосферное давление столбика"
    ждать  # "завтра ждать дождь"
    окружающей среды?
    оптимальная | средняя
    воздух
    очень
    если
    опять | снова
    а | не | или | ли | то
    всю
    прогноз
    прогнозировать
    предскаывать
    предсказать
    передавать
    %lemma off
    обещали | обещают
    %lemma on

    # аналоги слова "здесь" (но это не мешает захватывать регионы)
    и? (.? область | в? .? области | по .? области)

    # TODO(sparkle): убрать велосипеды в общий филлер
    я
    в яндексе
    (в|на|по)? гисметео
    на рамблере
    как найти
    хоть
    ого

    именно
    сюда
    быстро
    резко
    конкретно

$BydloFiller:
    епта
    ептеть
    ептить
    епт
    ежжи

$EllipsisFiller:
    то же самое?
    также
    это же
    еще раз

$PressureFiller:
    %lemma
    в? [миллиметры (ртутного столба)?]
    по ртутному? (столбу | столбику)?
    сила у?

$WindFiller:
    %lemma
    дуть

$RegionRu:
    %lemma
    адыгея
    алтай
    башкортостан
    бурятия
    дагестан
    ингушетия
    кабардино-балкария
    калмыкия
    карачаево-черкесия
    карелия
    коми
    крым
    марий эл
    мордовия
    саха
    якутия
    северная осетия-алания
    татарстан
    тыва
    удмуртия
    хакасия
    чечня
    чувашия
    алтайский край
    забайкальский край
    камчатский край
    краснодарский край
    красноярский край
    пермский край
    приморский край
    ставропольский край
    хабаровский край
    амурская область
    архангельская область
    астраханская область
    белгородская область
    брянская область
    владимирская область
    волгоградская область
    вологодская область
    воронежская область
    ивановская область
    иркутская область
    калининградская область
    калужская область
    кемеровская область
    кировская область
    костромская область
    курганская область
    курская область
    ленинградская область
    липецкая область
    магаданская область
    московская область
    мурманская область
    нижегородская область
    новгородская область
    новосибирская область
    омская область
    оренбургская область
    орловская область
    пензенская область
    псковская область
    ростовская область
    рязанская область
    самарская область
    саратовская область
    сахалинская область
    свердловская область
    смоленская область
    тамбовская область
    тверская область
    томская область
    тульская область
    тюменская область
    ульяновская область
    челябинская область
    ярославская область

$Country:
    %lemma
    австралия
    азербайджан
    соединенные штаты америки
    сша
    америка
    северная америка
    североамериканские штаты
    штаты
    армения
    белоруссия
    белорусь
    беларусь
    республика беларусь
    республика белорусь
    болгария
    бразилия
    великобритания
    англия
    британия
    шотландия
    северная ирландия
    соединенное королевство
    венгрия
    республика корея
    южная корея
    корея
    гонконг
    дания
    объединенные арабские эмираты
    оаэ
    эмираты
    египет
    индия
    индонезия
    иран
    исландия
    италия
    казахстан
    канада
    киргизия
    китай
    латвия
    литва
    малайзия
    мексика
    молдавия
    новая зеландия
    новозеландия
    зеландия
    израиль
    норвегия
    польша
    россия
    российская федерация
    рф
    раша
    рашка
    румыния
    саудовская аравия
    сингапур
    словакия
    таджикистан
    таиланд
    турция
    туркмения
    узбекистан
    узбекистон
    узбекистонда
    украина
    филиппины
    хорватия
    чехия
    швеция
    швейцария
    эстония
    южная африка
    япония


$Wind:
    ветрено
    безветрено
    %lemma
    ветер
    ветерок
    ветрище
    ветродуй
    ветерочек
    ветряга
    безветрие
    скорость ветра
    сила ветра
    (порыв | порывы) ветра

$Pressure:
    %lemma
    [атмосферное? давление]
    [давление воздуха]
    [давление в воздухе]
    [давление атмосферы]
    [давление в атмосфере]
    [воздушное давление]

$Temperature:
    %lemma
    температура
    [сколько градусов]
    [градусов тепла]
    градусы
