import: common/filler.grnt
import: weather/common.grnt

form personal_assistant.scenarios.get_weather__details:
    root:
        $MoreDetails

    $MoreDetails:
        [$More* $WeatherDetails* $Details+]

    $Details:
        %lemma
        конкретнее | поконкретнее
        конкретней | поконкретней
        подробнее | поподробнее
        подробней | поподробней
        инфы
        информации
        подробностей
        деталей
        в подробностях
        в деталях
        более детализированно
        (дай|покажи) более? подробную информацию
        (подробная | достоверная) информация
        узнать больше
        детально
        по часам | почасово
        поточнее
        это точно
        точно
        точная информация
        на более длительный срок

    $WeatherDetails:
        [$How? ($Weather.WeatherWord|$Weather.ExtraWeatherWord|$Weather.HeavyExtraWeatherWord)]

        $How:
            какой | какое | какая | какие

    $More:
        %lemma
        # $TellMe
        больше | побольше
        более
        покажи | скажи | расскажи
        полное
        узнать
        открой
        можно
        можешь
        дай

filler:
    $Common.Filler
    $Common.LeftFiller
    $Common.WayToCallAlice
    # $nonsense
    это
    мне
