import: common/adverbs.grnt
import: common/commands.grnt
import: common/filler.grnt
import: common/prepositions.grnt
import: common/pronouns.grnt
import: common/surfaces.grnt

form alice.proactivity.confirm:
    is_action: true
    root:
        $Common.LeftFiller* $Common.Agree+
        $Common.LeftFiller* [$Common.Play+ пока*]
        $Common.LeftFiller* [$Common.PlayAudio+ пока*]
        $Common.LeftFiller* [$Common.PlayGame+ пока*]
        $Proactivity.Agree+
        %negative
        $Common.LeftFiller* [давай+ пока+]
    filler:
        $Common.About
        $Common.Again
        $Common.Agree
        $Common.At+ $Common.SmartDevice*
        $Common.Filler
        $Common.More
        $Common.Now
        $Common.Play
        $Common.PlayAudio
        $Common.PlayGame
        $Common.Pronoun3Person
        $Common.PronounMe
        $Common.PronounMyself
        $Common.PronounPossessive3Person
        $Common.PronounThat
        $Common.PronounThis
        $Common.Still
        $Common.WayToCallAlice
        $Common.With
        $CustomFillers
        $RudeFillers
        будет
        только

        %negative
        один

$Proactivity.Agree:
    $Good:
        %lemma
        хороший
        отличный
        неплохой
        интересный
        прекрасный
        заманчивый
        замечательный

    $Grateful:
        %lemma
        рад
        благодарен
        признателен

    %lemma
    делай
    сделай
    попробуй
    расскажи
    [$Good идея]
    [$Good предложение]
    [быть $Grateful]

$CustomFillers:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    быть
    звучит
    очень
    тоже
    нормально
    жду
    не знаю
    скорее
    быстрее
    короче
    теперь

$RudeFillers:
    %lemma
    еб $Common.PronounYou? мать
    ебаный $Common.PronounYou? в? рот
    ебать $Common.PronounYou? в? рот?
