import: common/adjectives.grnt
import: common/adverbs.grnt
import: common/commands.grnt
import: common/filler.grnt
import: common/nouns.grnt
import: common/prepositions.grnt
import: common/pronouns.grnt
import: common/surfaces.grnt
import: common/verbs.grnt
import: player/commands.grnt
import: player/common.grnt


form personal_assistant.scenarios.player.like:
    root:
        $Common.ThePositiveFeedback+
        $Like.RootPhrase+

        %force_negative
        $Common.PronounYou+ $Common.ThePositiveFeedback+

    filler:
        $Common.Additionally
        $Common.At
        $Common.Filler
        $Common.For
        $Common.Lets
        $Common.MusicCharacteristic
        $Common.Now
        $Common.Personal
        $Common.Pronoun3Person
        $Common.PronounMe
        $Common.PronounThis
        $Common.Remember
        $Common.SmartDevice
        $Common.ThePositiveFeedback
        $Common.To
        $Common.Very
        $Common.WayToCallAlice
        $Common.Yes
        $Content
        $CoolWord
        $Liked
        $List
        $Player.DoLike
        $Player.ILike
        $nonsense
        %lemma
        как


# TODO(jan-fazli): Move to commands?
$Like.RootPhrase:
    $Inner:
        $Player.Add* $Common.At+ ($Liked | $MyFavourite)+
        $Player.Save+ $Common.To+ $Common.PronounMe+
        [$Common.PronounThis+ $Common.Favorite+ $Content+]
        [$Common.ToThank+ $Common.For+ $Something+]
        [$CoolWord+ ($Something | $Common.TheTaste)+]
        [$Player.Add+ (($Common.At | как)+ ($Liked | $MyFavourite | $List)+)+]
        [$Player.ILike+ $Content+]
        [$Player.Save+ ($Liked | $MyFavourite | $ThisItem)+]
        [$Player.Set+ $Content+ $Common.ThePositiveFeedback+]
        %lemma
        включи+ в+ ($Liked | $MyFavourite | $List)+

        %negative
        $Something+ $Common.QuestionWhat+ $Player.ILike+

    $Common.LeftFiller* $Inner+
    $Player.DoLike


$CoolWord:
    $Common.Cool.Singular
    $Common.Great.Singular
    $Common.Nice.Singular
    $Common.Normal.Singular


$List:
    $Common.ThePlaylist
    $Common.TheTrack
    %lemma
    аудиозаписи
    библиотека
    коллекция
    медиатека
    список


$Liked:
    $Inner:
        $Common.Favorite
        %lemma
        любить
        нравиться
        обожать
        предпочитать
        тащиться
        фанатеть

    %lemma
    [$Common.Very* $Inner+]


$MyFavourite:
    $Common.PronounMe+ ($Something | $List)+


$Content:
    $Common.TheSound
    $Something
    $ThisItem
