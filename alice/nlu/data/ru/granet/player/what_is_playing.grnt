import:
    common/commands.grnt
    common/filler.grnt
    common/nouns.grnt
    common/pronouns.grnt
    common/questions.grnt
    common/surfaces.grnt
    player/common.grnt


form alice.player.play_this_track:
    root:
        [$ThisTrack+ ($Common.Play|$Common.PlayAudio)+]


form personal_assistant.scenarios.player.what_is_playing:
    root:
        # кто это
        $Common.LeftFiller* [$Who+ $ObjPronoun+ $Filler*]

        # какая музыка
        $Common.LeftFiller* [$Which+ $Obj+ $Filler*]

        # что играет
        $Common.LeftFiller* [$Who+ ты? $IsPlaying+ $Filler*]

        # кого слушаем
        $Common.LeftFiller* [$Who+ $Listen+ $Filler*]

        # какую музыку слушаем
        $Common.LeftFiller* [$Which+ $Obj+ $Listen+ $NCFiller*]

        # как называется музыка|это
        $Common.LeftFiller* [$Common.QuestionHow+ $IsCalled+ ($Obj|$ObjPronoun)+ $NCFiller*]

        # название музыки
        $Common.LeftFiller* [$Common.TheName+ $Obj+ $NCFiller*]

        # слушай эту песню
        $Common.LeftFiller* [$Consider $Common.PronounThis+ $Obj+ $NCFiller*]

        %negative
        [$Common.Pronoun3Person+ что+ $NCFiller*]
        [что+ $Common.PronounMe+ $Common.PronounThis+ $NCFiller*]
        [ну? что (давай|играй|включай)+ $Obj+]

    $Consider:
        (послушай|слушай)
        определи
        (узнай|распознай)
        (найди|найти)
        угадай

    $Who:
        кто
        что
        кого

    $Which:
        %lemma
        какой
        который
        это? что за?
        чей

    $Obj:
        %lemma
        группа
        певец
        исполнитель
        артист
        солист
        песня
        песенка
        трек
        композиция
        произведение
        музыка
        музычка
        мелодия
        сказка
        аудио
        аудиокнига

    $ObjPronoun:
        $Common.Pronoun3Person
        $Common.PronounThis

    $IsPlaying:
        воспроизводится
        воспроизводишь
        воспроизводим
        воспроизводят
        играешь
        играем
        играет
        играют
        поет
        поют
        исполняется
        исполняет
        исполняем
        исполняют
        звучит
        звучат
        проигрывается
        проигрываешь
        проигрываем
        проигрывают

    $IsCalled:
        %lemma
        называется

    $Listen:
        слушаю
        слушаем

    $TellFind:
        $Common.Find
        $Common.Guess
        $Common.Recognize
        $Common.Tell
        подскажи
        распознай
        определи
        узнай
        определи
        ты? знаешь

    $Filler:
        $Common.At? $Common.SmartDevice
        $Common.Lets
        $Common.Now
        $Common.PronounMe
        $Common.WayToCallAlice
        $IsPlaying
        $Obj
        $ObjPronoun
        $TellFind
        $Which
        $Who
        слушай
        слышишь
        слышь
        знаешь
        %lemma
        вот
        привет
        тихо
        спасибо
        пожалуйста
        да
        за
        здесь
        у меня
        ну
        блин
        бля
        блять
        или
        и
        а
        еще
        раз
        окей
        такой
        вообще
        ой
        все? таки

    $NCFiller:
        $nonsense
        $Common.Filler
        $Filler
