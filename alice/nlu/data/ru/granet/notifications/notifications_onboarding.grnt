import: common/filler.grnt
import: common/pronouns.grnt
import: common/surfaces.grnt
import: notifications/common.grnt

form alice.notifications_onboarding:
    slots:
        wide_request:
            type:
                string
            source:
                $WideRequest
    root:
        [$Tell* $Why+ $Common.PronounMe* ($Speaker|$InSpeaker)+ $LightColor+]
        [$Speaker* $Get+ обновление+]
        [$Common.PronounMe* $Speaker* $Get+ $Message+]
        $WideRequest+
        [$Stop+ $Common.PronounMe+ подмигивать+]
    filler:
        %lemma
        для
        у
        там
        $Common.Filler
        $Common.WayToCallAlice
        $Common.LeftFiller

$Why:
    %lemma
    зачем
    из-за чего
    какого хрена
    по какой причине
    почему
    че
    чего
    что

$Speaker:
    $Common.PronounYou
    $Common.SmartSpeaker с? алисой?

$InSpeaker:
    [(у|в|на|из|с|от)+ $Common.PronounMe? $Speaker]

$Message:
    %lemma
    сообщение
    послание
    обновление

$Get:
    %lemma
    получила
    есть
    имеется

$Tell:
    %lemma
    расскажи
    скажи
    поведай
    поясни
    объясни

$Mean:
    %lemma
    означает
    значат
    значит
    обозначает

$Stop:
    %lemma
    хватит
    прекрати
    перестань

$WhyThisMean:
    [$Why+ ($Common.PronounThis | $Common.Pronoun3Person)?] ($Mean|за)+

$WideRequest:
    %lemma
    [$Tell* $Why+ $Common.PronounThis? $Common.PronounMe* ($Speaker|$InSpeaker)? ($Light|$LightSignal|$LightColor)+]
    [что+ то* случилось+]
    [в* чем+ дело+]
    [что+ то* ты+ хочешь+ сказать+]
    [что+ с+ тобой+]
    [ты+ тут+]
    [что+ ты+ делаешь+]
    [какого+ хрена+ происходит+]
    [$Tell* $WhyThisMean+ (который|когда)? ($Speaker|$InSpeaker)* ($Comes|$Light|$LightSignal|$LightColor)*]
