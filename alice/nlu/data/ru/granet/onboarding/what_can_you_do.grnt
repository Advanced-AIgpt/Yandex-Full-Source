import: common/all.grnt

form alice.onboarding.what_can_you_do:
    root:
        стоп ? нет? (кроме .)? $Root (кроме .+)?
        $AbilitiesNoLemma
        помощь

        
    filler:
        $Common.Filler
        $Common.WayToCallAlice
        $Common.LeftFiller
        еще
        о
        у
        в
        то
        про
        там
        список
        вообще
        расскажи о себе
        дальше
        я хочу? $Common.Ask
        $Common.Something
        $Common.Immediately
        $SynFillers
        $WhatYouCan
        $TellAboutYourAblilities

$SynFillers:
    %lemma
    %enable_synonyms all
    полностью
    поподробнее
    подробнее
    подробно
    и не только
    подожди
    интересного
    прикольного
    полезного
    классный
    клёвый
    свой

$Can:
    %lemma
    умеешь
    $Common.Can

$YouCanClear:
    [$Common.PronounYou? $Can]  ($Common.Do | Выполнять)?

$YouCan:
    [$Common.PronounYou? $Can]  ($Common.Do | Выполнять)?
    [есть $Common.PronounYou?]


$TellAbout:
    (покажи | расскажи | скажи | назови | перечисли | прочитай | зачитай)
    давай? (показывай | рассказывай | говори | называй)
    (можешь (показать | рассказать | сказать | назвать | прочитать | зачитать))
    
$Abilities:
    %lemma
    возможности
    способности
    функции
    навыки
    умения

$AbilitiesNoLemma:
    возможности
    способности
    функции
    навыки

$WhatYouCan:
    [$TellAbout* (том? $Common.QuestionWhat)+ $YouCanClear+ $YouCan* ($Common.Do|сделать)*]

$TellAboutYourAblilitiesInternal:
    [своих $Abilities+]
    [$Common.QuestionWhat $AbilitiesNoLemma]
    [$TellAbout $AbilitiesNoLemma]
    [(какие |что за) $Common.PronounYou?  $YouCan* $Abilities знаешь*]
    [на+ все? $Common.QuestionWhat+ $Common.PronounYou? способна]


$TellAboutYourAblilities:
    [$TellAbout* $Common.QuestionWhat* $TellAboutYourAblilitiesInternal]

$Root:
    [$WhatYouCan $Abilities?]
    $WhatYouCan
    $TellAboutYourAblilities