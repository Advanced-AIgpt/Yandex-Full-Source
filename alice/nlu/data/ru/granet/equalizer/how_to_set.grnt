import:
    common/adjectives.grnt
    common/commands.grnt
    common/filler.grnt
    common/questions.grnt
    common/verbs.grnt
    equalizer/common.grnt


form alice.equalizer.how_to_set:
    root:
        $Common.LeftFiller* [($QuestionHow|$Common.QuestionWhere)+ ($Activate|$ToFind|$IsLocated|$ToOpen)+ ($Equalizer|$SoundSettings)+]
        $Common.LeftFiller* [($Common.Teach|$Common.Help)+ $Activate+ ($Equalizer|$SoundSettings)+]
        $Common.LeftFiller* [($Common.Teach|$Common.Help)+ $SetExact+ $Sound+]
        $Common.LeftFiller* [($QuestionHow|$Common.QuestionWhere)+ $Tune+ $Sound+]
        $Common.LeftFiller* [($QuestionHow|$Common.QuestionWhere)+ $RaiseOrLower+ $HighOrLowFrequencies+]
        $Common.LeftFiller* [$Common.QuestionWhere+ $Equalizer+]
        $Common.LeftFiller* [$IDontLikeTheSound+ ($QuestionHow|$Common.QuestionWhere)+ $Tune+]

    filler:
        $Equalizer.CommonFiller
        $Equalizer.IWantToTuneTheSoundFiller
        $IDontLikeTheSound
        [($Common.TurnOn|$Common.TurnOff|$Common.ToListen) $Common.TheMusic]
        $Sound
        $Tune
        $Activate
        $Common.Help
        $Tell
        $Common.Teach
        $Common.ToLearn
        $Common.TheSettings
        $Common.Here
        $Instruction
        можно|могу|можем
        качество
        не нравится
        правильно
        лучше
        что такое
        с помощью

    $Set:
        $Common.Set
        %negative
        %lemma
        добавить

    $Activate:
        $Common.TurnOn
        $Common.ToUse
        $Set
        заиспользовать

        %negative
        %lemma
        добавить
        перейти
        %exact
        активируешь
        включишь
        врубишь
        запустишь

    $ToFind:
        найти
        отыскать
        поискать

    $ToOpen:
        попасть
        открыть
        зайти
        перейти
        добраться
        забраться
        войти
        оказаться
        очутиться
        выйти на

    $IsLocated:
        %lemma
        находится
        расположен

    $QuestionHow:
        $Common.QuestionHow
        можно
        %negative
        что

    $Tune:
        $Set
        (улучшить|усовершенствовать|подстроить|подкрутить)

    $RaiseOrLower:
        (увеличить|увеличивать|уменьшить|уменьшать|добавить|добавлять|прибавить|прибавлять|убавить|убавлять|усилить|усиливать|ослабить|ослаблять)
        $Set (громче|погромче|тише|потише|выше|повыше|ниже|пониже|больше|побольше|меньше|поменьше|сильнее|посильнее|слабее|послабее)

    $HighOrLowFrequencies:
        ($Common.High|$Common.Upper|$Common.Low|$Common.Lower) ($Common.TheSound|$Frequency)
        %lemma
        бас
        басс
        %exact
        (верха|верхов)

        $Frequency:
            %lemma
            частота

    $Sound:
        $Common.TheSound
        $HighOrLowFrequencies

    $IDontLikeTheSound:
        $DontLike:
            %lemma
            не нравится
            не заходит
            полный? (отстой|фигня|херня)
            (плохой|ужасный|отвратительный|невыносимый|дерьмовый|плоский|тусклый|отстойный|фиговый|херовый|хуевый|некорректный|нечеткий|размытый)

        [(совсем|абсолютно|полностью|очень|супер|(что|че|чего|какой|какая|какое|почему) то)* $DontLike $Sound]

    $Tell:
        %lemma
        расскажи
        скажи
        подскажи
        объясни
        покажи
        поведай
        уточни

    $SoundSettings:
        $Common.TheSettings $Sound

    $Instruction:
        %lemma
        (краткая|полная)? инструкция

    $SetExact:
        настроить
        поднастроить
