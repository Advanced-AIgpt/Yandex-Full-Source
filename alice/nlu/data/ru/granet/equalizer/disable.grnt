import:
    common/adjectives.grnt
    common/commands.grnt
    common/filler.grnt
    common/pronouns.grnt
    common/quit.grnt
    common/verbs.grnt
    equalizer/common.grnt


form alice.equalizer.disable:
    root:
        $Common.LeftFiller* [$LetOrMakeItSoThat* ($StopToWork|$Common.TurnOff)+ $Equalizer+]
        $Common.LeftFiller* [$LetOrMakeItSoThat+ (больше? не $Common.ToWork)+ $Equalizer+]
        $Common.LeftFiller* [(больше? $Common.DontWant|$Common.Remove|$Common.IAmTired)+ $Equalizer+]
        $Common.LeftFiller* [$Common.DontWant+ ($ToUseEqualizer|чтобы $EqualizerToBeTurnedOn)+]
        $Common.LeftFiller* [$LetOrMakeItSoThat+ $Common.ToListen+ $Common.TheMusic+ без $Equalizer]

    filler:
        $Common.IAmTired
        $Common.Stop
        $Equalizer.CommonFiller
        $TurnItOffFiller
        в? $Common.TheSettings $Common.TheSound
        чтобы больше не $Common.ToWork
        когда [ты? уже? наконец?]
        (тоже|также)

    $ToUseEqualizer:
        [$Common.ToUse+ $Equalizer+]

    $EqualizerToBeTurnedOn:
        [$Equalizer+ был* $Common.TurnedOn+]

    $StopToWork:
        $Common.ToStop ($Common.ToWork|$Common.TheWork)
        %lemma
        (стоп|замри)
        (останови|остановить|остановись)
        (обнули|обнулить)
        (убери|убрать)
        (сбрось|сбросить)
        (погаси|погасить)
        (отмени|отменить)

    $TurnItOffFiller:
        ($Common.Remove|$Common.TurnOff) $Common.PronounPossessive3Person?
