import:
    common/adverbs.grnt
    common/commands.grnt
    common/questions.grnt
    common/filler.grnt
    common/nouns.grnt
    common/prepositions.grnt
    common/pronouns.grnt
    common/surfaces.grnt
    common/verbs.grnt


$Equalizer:
    %lemma
    (все? (настройка звука?|функция)|режим)? музыкальный? эквалайзер (для (музыки|звука))? (и настройки (музыки|звука)?)?
    %exact
    (настройки|настройка|настройку) (звука|музыки)


$LetOrMakeItSoThat:
    ($Common.Let|$Common.Compel|$Common.MakeItSoThat|$Common.ToWant (чтоб|чтобы|что бы)?)


$OnTheSpeaker:
    на? $Common.SmartSpeaker


$OpenSettingsFiller:
    [$Open+ $Common.TheSettings+]

    $Open:
        $Common.ToOpen+
        (зайди|зайти) в


$ThereIsSmthOnMySpeaker:
    [(я знаю что?|у тебя|у меня|на колонке|если)* есть]


$Equalizer.CommonFiller:
    $nonsense
    $Common.Filler
    $Common.WayToCallAlice
    $Common.ToWant чтобы?
    $Common.PronounsAll
    $Common.At
    $Common.For
    ($Common.SmartSpeaker|$Common.SmartDevice.Device)
    $OpenSettingsFiller
    $Common.Find
    ($Common.ToHave|имеется)
    $Common.Now
    ($Common.In|у)
    $Common.QuestionHow
    $Common.QuestionWhere
    $Equalizer
    $Common.TheMusic
    $ThereIsSmthOnMySpeaker
    $Common.For $Common.PronounMe
    (можешь|не? могла бы|сделаешь)
    $Common.TheWork
    или
    чтоб
    (у себя|в себе)


$Equalizer.IWantToTuneTheSoundFiller:
    [$Common.ToWant* $Common.PronounMe* $Adjust+ ($Common.TheSound|$Common.PronounPossessive3Person)+ $ForSelf*]

    $Adjust:
        $Common.Set
        (улучшить|исправить|поправить|выправить|скорректировать|подкорректировать)

    $ForSelf:
        (под|для)+ себя+


$BasicSoundSettings:
    %lemma
    $Common.TheSettings ($Common.TheSound|музыка)


$Bass:
    %lemma
    (бас|басс|басик)
    (громкость|звук|количество|интенсивность|уровень)? (баса|басса|низких частот|нижних частот|низкочастотных гармоник)
    басовитее
