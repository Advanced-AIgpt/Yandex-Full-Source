import: common/filler.grnt
import: general_conversation/movie_entities.grnt


form alice.movie_discuss:
    filler:
        $Common.Filler
        $Common.LeftFiller
        $nonsense
    root:
        .* $MovieName .*
        %negative
        %lemma
        .* дом два .*
    slots:
        film_id:
            type:
                entity_search.film
                string
            source: $MovieName


form alice.game_discuss:
    filler:
        $Common.Filler
        $Common.LeftFiller
        $nonsense
    root:
        .* $GameName .*
        %negative
    slots:
        game_id:
            type:
                entity_search.soft
                string
            source: $GameName


form alice.music_discuss:
    filler:
        $Common.Filler
        $Common.LeftFiller
        $nonsense
    root:
        .* $MusicName .*
        %negative
    slots:
        music_id:
            type:
                entity_search.music
                entity_search.band
                string
            source: $MusicName


$MovieName:
    $entity_search.film
    $MovieEntity


$GameName:
    $entity_search.soft


$MusicName:
    $entity_search.music
    $entity_search.band

    %negative
    %lemma
    .* нравится .*
    .* не люблю .*
    .* а тебе .*
