import: common/commands.grnt
import: common/filler.grnt

form alice.general_conversation.proactivity.bored:
    root:
        [((когда|если)? $ImBored)? ((предложи|предложите)? $WhatToDo)]
        [((когда|если)? $ImBored) ((предложи|предложите)? $WhatToDo)?]
        [((когда|если)? $ImBored) $WhatToDoSimple]
        [$ImBored? $Entertain]

        %negative
        .* $Play .*
        .* $Talk .*
        .* $ImBoredToDoSomething .*
        .* $ImBoredAndWantToDo .*
        .* $AliceDo .*
        .* $AliceCommands .*

    filler:
        $Common.Filler
        $Common.LeftFiller
        $Common.Lets
        $Common.PronounMe
        $Common.WayToCallAlice
        $CommonFiller
        вдвоем
        втроем
        компанией

form alice.general_conversation.proactivity.alice_do:
    root:
        $AliceDo $AliceDo?
        [$AliceBored $AliceDo?]

    filler:
        $Common.Filler
        $Common.LeftFiller
        $Common.PronounYou
        $Common.WayToCallAlice
        $CommonFiller

$CommonFiller:
    на
    в
    у
    чем нибудь
    что нибудь
    нибудь
    ладно
    не знаю
    мне
    тоже
    очень
    сильно
    то
    ж
    дома
    даже
    что то
    что
    как то
    как
    бы
    совсем
    еще
    короче
    [(скажите|скажи)? пожалуйста]
    эх
    не знаю
    господи
    да

$AliceCommands:
    лучше? займи
    лучше? включи
    лучше? запусти
    лучше? помоги
    ты? знаешь как?
$AliceBored:
    [(ты|вы) не? устала $When? $Where?]
    [(ты|вы)? скучаешь $When? $Where?]
$AliceDo:
    [какие планы $When? $Where? (тебя|вас)?]
    [(что|чем)? (ты|вы)? (хочешь|хотите|будешь|будете|планируешь|планируете) (делать|заняться|заниматься) $When? $Where?]
    [(что|чего)? (ты|вы)? [(любишь|любите) делать] $When? $Where?]
    [(что|чего) (ты|вы)? (делаешь|делаете|делала) $When? $Where?]
    [чем (ты|вы)? (займешься|займетесь|занимаешься|занимаетесь) $When? $Where?]
    [как $When? $Where? [планируешь|планируету] провести]
$WhatToDo:
    [посоветуй? (нечем|чем то| чем) $When? заняться]
    [нечего $When? (делать|поделать|сделать)]
    [(думаю|думаем)? как провести $When?]
    [(думаю|думаем)? чем нибудь? $When? (заняться|занять|займемся)]
    [(устал|устала) $When?]
    [посоветуй? как провести $When?]
    [посоветуй? чем занять $When? $Somebody]
$WhatToDoSimple:
    [(думаю|думаем)? что $When? (делать|поделать|сделать)]
$When:
    теперь
    дальше
    ((это|эта|этим|этой)? (завтра|сегодня|выходные|выходных|празниках)) $WhenPartDay?
    потом
    сейчас
    праздники
    выходные
    свободное время
    $WhenPartDay
$WhenPartDay:
    (это|эта|этим|этой)? (утро|утром)
    (это|эта|этим|этой)? (день|днем)
    (это|эта|этим|этой)? (вечер|вечером)
    (это|эта|этим|этой)? (ночь|ночью)
$Where:
    у себя
    без меня
    дома
$ImBored:
    грустно
    не весело
    устал|устала
    %lemma
    скучно
    скучать
    скука
    скучновато
    надоело
    грусть
$Entertain:
    ты? [(займи|развесели|развлеки|повесели|развлекай) $Somebody?]
    [как? развлечься]
    [как? развеселиться]
$Somebody:
    меня
    ребенка
    сына
    дочку
    маму
    мать
    папу
    отца
    мужа
    жену
    тещу
    тестя
$Talk:
    %lemma
    говорить
    поговорить
    поболтаем
    разговаривать
$Play:
    %lemma
    игра
    играть
    сыграть
    поиграть
$Watch:
    %lemma
    [смотреть $ContentType?]
    [посмотреть $ContentType?]
    $ContentType
$ContentType:
    $Movie|$Cartoon
$Movie:
    %lemma
    фильм
    кино
    кинофильм
    киношка
    кинцо
    кинчик
    полнометражка
    полнометражный фильм
$Cartoon:
    %lemma
    мульт
    мультик
    мультфильм
    мультипликационный фильм
    анимация
    анимационный фильм
$Listen:
    %lemma
    [слушать $Music?]
    [послушать $Music?]
    $Music
$Music:
    %lemma
    музыка
    музон
    музяка
    музло
$Want:
    $Common.Lets
    включи
$DontWant:
    не хочу
$ImBoredToDoSomething:
    ($ImBored|$WhatToDo) $DontWant? ($Play|$Talk|$Listen|$Watch)
    $DontWant? ($Play|$Talk|$Listen|$Watch) .* ($ImBored|$WhatToDo)
$ImBoredAndWantToDo:
    $ImBored $Want ($Play|$Talk|$Listen|$Watch)
    $Want ($Play|$Talk|$Listen|$Watch) .* $ImBored
