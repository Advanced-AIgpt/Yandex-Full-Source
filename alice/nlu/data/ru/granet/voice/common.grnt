import: common/commands.grnt
import: common/conjunctions.grnt
import: common/filler.grnt
import: common/prepositions.grnt
import: common/pronouns.grnt
import: common/questions.grnt
import: common/surfaces.grnt
import: common/tense.grnt

$Speak:
    $Common.Speak
    $Common.Respond

$TurnOn:
    $Common.Agree
    $Common.Lets
    $Common.Set
    $Common.TurnOn
    $Speak

$TurnOff:
    $Common.CancelRemoveReset
    $Common.Decline
    $Common.Exit
    $Common.No+ $TurnOn
    $Common.Stop
    $Common.TurnOff

$Whisper:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    пошептаться
    шепот
    шептать

$NonWhisper:
    $Normal:
        %lemma
        %enable_synonyms all
        %disable_synonyms synset
        нормально
        нормальный
        обкновенно
        обыкновенный
        обычно
        обычный
        привычно
        привычный
        стандартно
        стандартный

    $Voice:
        %lemma
        %enable_synonyms all
        %disable_synonyms synset
        вслух
        голос

        %negative
        слушаться

    %fillers off
    [$Speak+ как+ $Normal+]
    [$Speak+ $Normal* в* $Voice+]

    %force_negative
    .* не скажешь .*
    .* мой голос .*
    .* голосом говоришь .*

$Setting:
    %lemma
    %enable_synonyms all
    возможность
    навык
    настраивать
    настроить
    настройка
    опция
    режим
    функционал
    функция

$Can:
    %lemma
    умеешь
    можешь

$Explain:
    %lemma
    для чего нужен
    зачем
    [как (выглядит | работает | $Common.TurnOn | $Common.TurnOff)]
    объясни
    описание
    почему
    расскажи
    что произойдёт
    что значит
    что за
    что из себя представляет
    что такое

    $Can

$Show:
    ответь
    скажи

    %lemma
    включи пример
    демонстрация
    звучать
    изобрази
    как
    позови
    покажи
    послушать
    пример
    продемонстрируй
    прошептать
    тестирование
    тестировать
    услышать
    фраза

$SpeakWhisper:
    %lemma
    перешептываться
    пошептать
    пошепчись
    прошептать

$Phrase:
    %lemma
    %enable_synonyms all
    предложение
    фразу

    $Common.Something

$Filler:
    %lemma
    %enable_synonyms all
    больше
    вопрос
    голос
    громко
    используй
    надо
    начать
    никогда
    обычный
    ответ
    речь
    тоже
    чтобы
    у
    будешь

    $Common.About
    $Common.Ask
    $Common.At
    $Common.ConjunctionIf
    $Common.ConjunctionThen
    $Common.Filler
    $Common.From
    $Common.Lets
    $Common.PronounMe
    $Common.PronounThat
    $Common.PronounPossessive3Person
    $Common.PronounYou
    $Common.QuestionWhere
    $Common.SmartDevice
    $Common.Tell
    $Common.Tense
    $Common.WayToCallAlice
    $Setting
    $nonsense
