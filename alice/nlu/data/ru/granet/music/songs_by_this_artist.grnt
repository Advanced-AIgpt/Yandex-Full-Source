import: common/adverbs.grnt
import: common/commands.grnt
import: common/filler.grnt
import: common/nouns.grnt
import: common/prepositions.grnt
import: common/pronouns.grnt
import: common/questions.grnt
import: common/surfaces.grnt
import: common/verbs.grnt
import: music/commands.grnt
import: music/common.grnt

form alice.music.songs_by_this_artist:
    root:
        ($Common.LeftFiller|$Common.Lets)* [$PlayFindContinue+ $Songs* $ThisArtist+]
        ($Common.LeftFiller|$Common.Lets)* [$PlayFindContinue* $Songs+ $Artist+]
        ($Common.LeftFiller|$Common.Lets)* [$PlayFindContinue* $Other+ $Songs+ $ThisArtist+]
        ($Common.LeftFiller|$Common.Lets)* [$Common.QuestionWhat* $Songs+ $Artist+ ($Common.ToHave|есть)+]

        %negative
        ($Common.LeftFiller|$Common.Lets)* [$PlayFindContinue* $Songs* $ArtistAlice+]
        ($Common.LeftFiller|$Common.Lets)* [$Common.QuestionWhat* $Songs* $ArtistAlice+ ($Common.ToHave|есть)+]
    filler:
        $Common.Filler
        $Common.WayToCallAlice
        $Common.SmartSpeaker
        $Common.PronounThat
        $Common.PronounThis
        $Common.Additionally
        $Common.From
        $Common.Can
        $Common.Something
        $Music.Playing
        дальше
        у

$PlayFindContinue:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    $Music.Play
    $Common.Find
    $Common.Continue

$Other:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    $Common.Another
    $Another
    остальные
    все

$Songs:
    $Other* ($Common.TheTrack|$Common.TheMusic)+

$Artist:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    [($Common.PronounThat|$Common.PronounThis)* $Common.TheArtist+]

$ThisArtist:
    %lemma
    %enable_synonyms all
    %disable_synonyms synset
    [($Common.PronounThat|$Common.PronounThis)+ $Common.TheArtist+]

$ArtistAlice:
    $Artist+ $Common.WayToCallAlice+
