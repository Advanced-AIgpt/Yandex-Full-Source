import: common/commands.grnt
import: common/filler.grnt
import: common/persons.grnt

form personal_assistant.scenarios.music_ambient_sound:
    root:
        [$Action* $Item]

$Action:
    $Common.Play
    $Common.PlayAudio
    %lemma
    включить
    включать
    создать
    дать
    давать
    запустить
    (заснуть|уснуть) под
    издать
    поиграть
    играть
    сыграть
    изобразить
    имитировать
    сделать
    найти
    открыть
    произнести
    послушать
    слышать
    слушать
    услышать
    воспроизвести
    поставить
    показать

$ItemModifier:
    %lemma
    природа
    релаксация
    релакс
    исцеляющие
    расслабляющие
    успокаивающие
    подборка
    плейлист

$ItemType:
    как (шумит|звучит|звучат)
    %lemma
    звук|(со звуками)
    шум|(с шумом)
    гул
    раскат|(с раскатами)
    треск
    звучание

$ItemFiller:
    [$ItemModifier* $ItemType]

$Item:
    [$ItemFiller* $AmbientSoundSlot]
    [$ItemFiller+ $MaybeAmbientSoundSlot]


$AmbientSoundSlot:
    $AmbientSounds
    $custom.ambient_sound
    [$AmbientSound $AmbientSoundsAttribute*]

$MaybeAmbientSoundSlot:
    [$MaybeAmbientSound $AmbientSoundsAttribute*]

$AmbientSounds:
    $AmbientSound и? $AmbientSound

$AmbientSoundsAttribute:
    %lemma
    ночное
    ночью
    утреннее
    утром
    дневной
    днем
    вечером
    вечерний
    в (лесу | городе | джунглях | саду)
    (с | и) пением? (птичками|птицами)
    (с | и) (чайки|чаек)
    красивый
    весной | осенью | зимой | летом
    весенний | осенний | летний | зимний
    на рассвете
    с утра

$AmbientSound:
    %lemma
    (белый|приятный|черный|синий|серый|зеленый) шум? дождя?
    [(собак|собаки|собака) (лает|гавкает|вой|воет|рычит|рычание)? злая?]
    стук колес
    гудок (поезда|паровоза)
    лес после дождя | (дождь|гроза) в лесу
    ночные звуки
    (успокаивающие | расслабляющие | спокойные) звуки
    природные звуки
    страшные звуки
    мягкий шум
    (помяукай|помурчи) как кошка
    шипение кошки
    городской шум
    пение птиц (жаворонок | соловей | канарейка)
    [(мяукающий | дерущихся)? $Cat]
    [(мяуканье|мурчание|мурлыканье|мурлыкание|урчание) ($Cat|кошачье)?]
    [(голос|пение) ($Cat|кошачье)]
    [(мяукать|мурчать|мурлыкать|мурлычет|мяучет|кричит|орет)? $Cat]
    [(собака|щенок|щенята|собачка|пёс) (лает|гавкает|лай|гавканье|голос)?]
    треск? горящий? костер
    горящий? камин
    [(чириканье|щебетание|пение|хищных|поют|чирикают|щебечут)? (птиц|птичек) (в лесу)?]
    [(журчания|журчащей|журчит|шумит)? (воды|речки|ручейка)]
    морской шум
    море (с чайками)?
    морская волна
    [морской прибой]
    прибой моря
    океан
    (летний|сильный)? дождь (по крыше)?
    капает? дождь
    капли дождя
    дождь без (грозы|грома|молнии)
    сильная? гроза
    гром с (молнией|дождем|грозой)
    дождь с (грозой|громом|молнией)
    гроза с (дождем|громом|молнией)
    лесная природа
    (хвойный|вечерний|утренний|весенний|летний|осенний|зимний|дикий)? лес
    в лесу
    (лесные|лесной) (звуки|шум)
    %negative
    шум

$MaybeAmbientSound:
    # this node requires $ItemType ("шум" etc.) to be present
    %lemma
    льва|лисы|медведя|попугаев|слона|коровы|тигра|дельфина|динозавра|гуся|щенят|животных|животного|курицы
    петуха|обезьяны|зверей|сверчков|лошади|волка|совы|касатки|комара|косули|зомби|(морских свинок)
    [полицейской? машины]
    (взлета|посадки)? (взлетающего|реактивного)? (трактора|мотоцикла|вертолета|двигателя|мотор|самолета)
    автомата|выстрела|пистолета|взрыва|
    из ада
    электрошокера
    школьного звонка
    большого? (вечернего|ночного)? (урбана|города)
    (спокойного|тихого)? дождя|молнии|грозы|грома|волн|ветра|моря|океана|ливня|прибоя
    смеха
    полиции
    тревоги
    дарт вейдера
    [(рокот|грохот|грохочущий|ниагарский)? водопад]
    дрели
    космоса
    тишины
    телефонного звонка
    ночи
    пылесоса
    фанфар
    [сирены (полиции|полицейской|милицейской|пожарной|ядерной|скорой помощи)?]
    [сигнализации (полиции|полицейской|милицейской|пожарной|ядерной|скорой помощи)?]
    морской котик
    вода из под крана
    воздушной тревоги
    паровоза в купе
    оптимуса прайма
    дикой? ночной? тропической? южной? дикой? природы
    (битого|бьющегося) стекла
    ситара
    свиста|колоколов|барабан|саксофона
    погремушки
    капли|капельки|капелек
    аплодисментов
    сигнализации
    (бьют куранты) | (бой курантов)
    ночного леса
    ночного города
    бритвы
    (тропический|русский|ночной|дневной) лес
    тропический (дождь|ливень)
    для (сладкого|крепкого|спокойного)? (сна | засыпания | релаксации | расслабления | исцеления)
    после дождя
    горной реки

$Cat:
    %lemma
    кошка|кот|котята|котик|котики|кошечки

filler:
    $Common.Agree
    $Common.Filler
    $Common.Lets
    $Common.PronounMe
    $Common.PronounPossessive3Person
    $Common.WayToCallAlice
    $nonsense
    вот|так|то|под
    снова
    для (сна|засыпания)? ($Common.Children|работы|сосредоточения)?
    (без рекламы)|бесплатно|онлайн
    на ночь
    природы
    лучше
    перед сном
    запись
    как
