import: common/filler.grnt
import: common/commands.grnt

form personal_assistant.scenarios.radio_play_post:
    root:
        $RootPositive
        %negative
        $RootNegative

$RootPositive:
    [$TurnOn* $Radio+ $Station? $Fm*]
    [$Radio+ $Station $Fm*]
    [$TurnOn+ $Station $Fm*]
    [$Station $Fm]
    [$TurnOn* $Fm]
    $ConfidentStationName

$RootNegative:
    [$TurnOn* $NotOnlyStation]

filler:
    $nonsense
    $Common.Filler
    онлайн
    на
    любое
    какое-нибудь
    какое нибудь

$Station:
    $StationName
    $Frequency

$Frequency:
    $custom.fm_radio_freq

$StationName:
    $custom.fm_radio_station
    $ConfidentStationName

$ConfidentStationName:
    # todo: make sure they are substituted with custom entity in the slot if possible
    %lemma
    европа плюс
    europa plus
    авторадио
    эхо москвы
    серебряный дождь
    милицейская волна
    dfm
    монте карло
    говорит москва
    эльдорадио

$TurnOn:
    $Common.Play
    $Common.PlayAudio
    найди

$Radio:
    %lemma
    [$Fm? интернет? $RadioNarrow]

$RadioNarrow:
    %lemma
    radio
    радио
    радиостанция
    радио станция

$Fm:
    фм
    fm

$NotOnlyStation:
    %lemma
    алиса
    один|первый|1
    два|второй|2
    три|третий|3
    $sys.num
    лайк
    $Genre

$Genre:
    $custom.genre
