import: common/filler.grnt

# the former personal_assistant.scenarios.music_play_anaphora
form personal_assistant.scenarios.music_play_anaphora:
    slots:
        action_request:
            source:
                $Launch
            type:
                custom.action_request
                string
        repeat:
            source:
                $Repeated
            type:
                custom.repeat
                string
        target_type:
            source:
                $TrackTagged
                $AlbumTagged
                $ArtistTagged
            type:
                custom.target_type
                string
        need_similar:
            source:
                $NeedSimilar
                $NeedSimilarTransitive
            type:
                custom.need_similar
                string
        order:
            source:
                $Randomly
                $Shuffle
            type:
                custom.order
                string
    root:
        $RepeatPhrase
        $ShufflePhrase
        $ThisArtistPhrase

filler:
    $nonsense
    $Common.Filler
    %lemma
    мне
    если не сложно
    плиз
    пожалуйста
    а? давай
    весь

$Launch:
    %type "custom.action_request"
    %value "autoplay"
    запусти
    запустить
    играй
    проиграй
    играть
    плей
    play
    поставь
    поставить
    вруби
    врубай
    слушать
    послушать
    включи
    включай
    включить
    включите
    сыграй
    зачитай
    петь
    поищи
    пой
    пойте
    послушаем
    прочитай
    услышать
    прослушать
    спеть
    спой
    спойте
    слышать
    читай
    воспроизведи
    воспроизвести
    прослушать и скачать
    послушать и скачать
    слушать и скачать
    скачать и слушать
    скачать и послушать

$LaunchGeneral:
    $Launch
    # todo: discuss the status of the following actions
    загрузить
    закачай
    найди
    открой
    поищи
    покажи
    скачай

$ItAccusative:
    его
    ее
    их

$This:
    эта
    эти
    это
    этот

$ThisAccusative:
    (эту|ту)  же?
    (этого|того)  же?
    (этот|тот)  же?
    (этих|тех)  же?

$ThisGenitive:
    (этой|той) же?
    (этого|того) же?
    (этих|тех) же?

$Repeated:
    %type "custom.repeat"
    %value "repeat"
    на повторе
    на репите
    в цикле

    без остановки
    нон стоп
    на повтор

$Music:
    %lemma
    музыка
    музон
    музяка
    музло

$Track:
    %lemma
    песня
    трэк
    трек
    track
    хит
    песенка
    мелодия
    композиция
    произведение
    запись
    рингтон

$TrackTagged:
    %type "custom.target_type"
    %value "track"
    $Track

$Album:
    %lemma
    альбом
    пластинка
    диск

$AlbumTagged:
    %type "custom.target_type"
    %value "album"
    $Album

$Artist:
    %lemma
    группу
    группировку
    исполнителя
    исполнительницу
    бэнд
    band
    команду
    виа
    музыкальный коллектив
    ансамбль
    артиста
    артистку
    певца
    певицу
    исполнителей
    исполнителиц
    певцов
    певиц

$ArtistTagged:
    %type "custom.target_type"
    %value "artist"
    $Artist

$MusicType:
    $Track
    $Album
    $Artist

$MusicTypeTagged:
    $TrackTagged
    $AlbumTagged
    $ArtistTagged

$NeedSimilar:
    %type "custom.need_similar"
    %value "need_similar"
    %lemma
    аналогичное
    аналогичного
    аналогичную
    аналогичный
    аналогичные
    аналогичных
    подобное
    подобного
    подобную
    подобный
    подобные
    подобных
    похожее
    похожего
    похожий
    похожую
    похожие
    похожих
    схожее
    схожего
    схожий
    схожую
    схожие
    схожих

$NeedSimilarTransitive:
    %type "custom.need_similar"
    %value "need_similar"
    типа
    вроде
    как

$NeedSimilarGeneral:
    $NeedSimilar
    $NeedSimilarTransitive

$SimilarToThis:
    $NeedSimilar этому?
    $NeedSimilar $ThisTrack

$ThisTrack:
    %lemma  # really need only accusative and dative cases
    эта песня
    этот трэк
    этот трек
    этот track
    этот хит
    эта песенка
    эта мелодия
    эта композиция
    это произведение
    эта запись
    этот звук
    этот шум

$Randomly:
    %type "custom.order"
    %value "shuffle"
    в любом порядке
    в перемешку
    в произвольном порядке
    в рандомном порядке
    в случайном порядке
    в смешанном порядке
    вперемешку
    на рандоме
    на шафле
    рандомно
    шафлом

$Shuffle:
    %type "custom.order"
    %value "shuffle"
    замешай
    перемешай
    перемешать

$Object:
    $ItAccusative
    $ItAccusative $Music
    $ItAccusative $MusicTypeTagged
    $ThisAccusative $MusicTypeTagged
    что-нибудь $SimilarToThis

$RepeatPhrase:
    [$Launch $Object $Repeated]
    #$Launch $ItAccusative $Repeated
    #$Launch [($ItAccusative $Music) $Repeated]
    #$Launch [($ItAccusative $MusicType) $Repeated]
    #$Launch [($ThisAccusative $MusicType) $Repeated]
    #[что-нибудь $SimilarToThis $Launch $Repeated]

$ShufflePhrase:
    [$Launch $Object $Randomly]
    [$Launch $Object $Randomly? (и $Shuffle)]

$SimilarToThisSuffix:
    на (эту | этот | это | эти)
    этому | этой | этим
    с (этим  | этой | этими)
    этого | этой | этих

$SimilarToThatSuffix:
    на (ту | тот | то | те)
    тому | той | тем
    с (тем  | той | теми)
    того | той | тех

$ThisArtistPhrase:
    [($LaunchGeneral мне?) $ThisArtistGroup]

$ThisArtistGroup:
    # "включи их музыку", исполнитель из анафоры
    $ItAccusative $Music
    $ItAccusative $MusicTypeTagged
    $ThisAccusative ($ArtistTagged|$AlbumTagged)
    # "включи эту группу", исполнитель/альбом/трек из анафоры или контекста
    $This ($ArtistTagged|$AlbumTagged)
    # "включи музыку этой группы", исполнитель из анафоры или контекста
    ($Music | $Track | $Album) $ThisGenitive $ArtistTagged
    ($Music | $Track) (с | из) $ThisGenitive $AlbumTagged
    # похожее
    # включи похожую музыку
    $NeedSimilar
    $NeedSimilar ($Music | $MusicTypeTagged)
    ($Music | $TrackTagged) $NeedSimilar $SimilarToThisSuffix

    # включи похожее на них (анафора)
    $NeedSimilar на (него|нее|них)
    $NeedSimilar (ему | ей | им)
    [$NeedSimilar ($SimilarToThisSuffix ($Artist | $Album | $Track))]

    ($MusicTypeTagged|$Music) $NeedSimilarGeneral на? ((нее|него|них)|$ThisGenitive|$This)
    $NeedSimilarGeneral на? ((нее|него|них)|$ThisGenitive|$This) ($MusicTypeTagged|$Music)

    ($MusicTypeTagged|$Music|что-нибудь)? $NeedSimilarGeneral $SimilarToThatSuffix что сейчас играет
    ($MusicTypeTagged|$Music|что-нибудь)? $NeedSimilar
    ($MusicTypeTagged|$Music|что-нибудь)? $NeedSimilarGeneral на? $ThisTrack
