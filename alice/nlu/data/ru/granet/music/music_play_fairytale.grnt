import: common/filler.grnt
import: common/commands.grnt
import: common/pronouns.grnt

form personal_assistant.scenarios.music_play_fairytale:
    slots:
        fairytale_theme:
            type: custom.fairytale_theme
            source: $Bedtime

    root:
        [ты? $Tell+ $FairyTaleGroup+]
        $FairyTaleGroup
        %negative
        $NotFairyTale

    filler:
        $Common.Filler
        $Someone
        $Common.PronounMe
        а
        для
        можно
        %lemma
        $Intro
        %negative
        ты

$Intro:
    привет
    добрый вечер
    доброе утро
    добрый день
    хай
    здарова
    ты можешь
    пожалуйста
    плиз
    если не трудно
    лучше

$FairyTaleGroup:
    [$Attribute* $Fairytale $Bedtime*]
$Attribute:
    %lemma
    добрая
    интересная
    спокойная
    успокаивающая
    детская
    для малышей
    для детей
    какая-то
    какая то
    какая-нибудь
    какая нибудь
    любая
$Fairytale:
    %lemma
    сказка
    сказочка
    аудиосказка
$Tell:
    $Common.Play
    $Common.PlayAudio
    $Common.Search
    $Common.Lets
    %lemma
    давай все таки
    послушаем
    почитай
    прочитай
    прочитать
    расскажи
    расскажите
    рассказывай
    скажи
    я? хочу
    ты? $Modal $ToTell
$Modal:
    можешь
    начинай
    будешь
    сможешь
$ToTell:
    рассказать
    рассказывать

$NotFairyTale:
    $PlayerCommand
    .* однажды в сказке .*
    .* как в старой сказке .*
    $Tell+ $Music .*
$PlayerCommand:
    [$Tell* $Another $Fairytale*]
    [$Stop $Fairytale*]
$Another:
    %lemma
    ещё
    еще
    следующая
    другую
    другая
    предыдущая
    прошлая
$Stop:
    %lemma
    выключи
    останови
    продолжи
    продолжай
$Music:
    подборку
    песню
$Bedtime:
    %type "custom.fairytale_theme"
    %value "bedtime"
    $ForSleeping+
$ForSleeping:
    на ночь
    перед сном
    для $GoodSleep? (засыпания|сна)

    %lemma
    (нужно|чтобы)? [$LayDown $Someone? $ToSleep?]
    чтобы [$Someone? $Fast? $ToSleep]
$LayDown:
    %lemma
    лег
    лечь
    ложиться
    уложить
$ToSleep:
    %lemma
    заснуть
    уснуть
    спать
    cпал
    засыпать
    уснул
    уснули
$GoodSleep:
    %lemma
    легкий
    крепкий
    хороший
    сладкий
    быстрый
$Fast:
    быстро
    поскорее
    побыстрее
    быстренько
    легко
    крепко
$Someone:
    %lemma
    он
    она
    им
    ребенок
    дети
    малыш
    малыши
