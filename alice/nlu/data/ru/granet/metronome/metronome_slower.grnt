import: common/adverbs.grnt
import: common/commands.grnt
import: common/verbs.grnt
import: metronome/common.grnt

form alice.metronome.slower.ifexp.bg_exp_enable_metronome:
    lemma: true

    slots:
        bpm_shift:
            source: $Metronome.BPMNumberShift
            type: sys.num
        bpm_exact:
            source: $Metronome.BPMNumberExact
            type: sys.num
        bpm_uncertain_shift:
            source: $Metronome.ShiftExtent
            type: metronome.shift_extent

    root:
        [$Metronome.ShiftExtent* $SlowDownWord+ $Metronome.Metronome* $Metronome.BPMChange*]
        [$Metronome.ShiftExtent* $Decrease+ ($Metronome.TempoSynonym|$Metronome.Metronome)+ $Metronome.BPMChange*]

    filler: $Filler

    $SlowDownWord:
        %lemma
        замедлить
        замедлиться
        медленнее|помедленнее
        притормози
        сбавь

        %negative
        %lemma off
        медленно

    $Decrease:
        %lemma
        $SlowDownWord
        снизить
        убавить
        понизить
        отбавить
        уменьшить
        реже|пореже
        меньше|поменьше
        понизь
        ниже

    $Filler:
        %lemma
        $Metronome.ChangeTempoFiller
        $TooFast

    $TooFast:
        %lemma
        $Common.Very (быстро|быстрый)
        не так быстро
