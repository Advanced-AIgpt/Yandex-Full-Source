import: news/smi.grnt
import: news/rubric.grnt
import: common/commands.grnt
import: common/filler.grnt
import: common/pronouns.grnt
import: common/verbs.grnt


form personal_assistant.scenarios.get_news:
    slots:
        topic:
            source: $ArbitraryTopic|$UnsureArbitraryTopic|$Rubric|$Smi|$Fio|$NewsIncident
            type:
                custom.news_topic
                string
        where:
            source: $WhereNoun
            type: string
        where_id:
            source: $WhereNoun
            type:
                GeoAddr.Address
                sys.geo
    root:
        $Root
    filler:
        $Common.Filler
$Root:
    [$Common.LeftFiller* $Common.Lets* $LeftSpeech* $nonsense*] $NewsRoot
    %negative
    %weight 100000000
    [.* видео]
    [.* как здоровье]
    [.* как включить]
    [.* навык]
    [.* сонник]
    [.* хочешь]
    [.* читала]
    [.* (для тебя|у тебя|у меня|у вас|о вас|о тебе|мою|твою)]
    [.* таймер]
    .* [интернета нет] .*
    .* у меня .? $News .*
    .* как будет .* по .*
    .* как по . .? будет .*
    .* как переводится .*
    .* в жизни .*
    .* с интернетом .*
    .* стабильности нет .*
    .* мир меняется .*
    [вести (себя|авто|машину|автомобиль|транспорт|уроки) .*]
    .* как написать .*
    .* как пишется .*
    .* не ($UnsureNews|$Tell|$SmiTell) .*
    [события истории .*]
    [$HappensInPast $sys.datetime+ .*]
    [события $sys.datetime+ .*]
    %lemma
    [события (книга|роман|рассказ) .*]
    [$HappensInPast (книга|роман|рассказ) .*]
    .* обстановка по кайфу .*
    .* (включи|поставь) (кино|ролик|фильм|музыку|песню|клип|серию|сериал) .*
    .* не $Common.ToWant .*
    [.* (канал|телеканал|подкаст)]
    [.* (обстановка|ситуация) (на дорогах|дорожная)]
    .* $What .? .? .? посмотреть .*

$NewsRoot:
    [$Summary? $WhatsUpNews  $Where  $AnyTopic?       $NotNewsSure*]
    [$Summary? $WhatsUpNews          $Topic           $NotNewsSure*]
    [$Summary? $UnsureNews   $Where  $AnyTopic?       $NotNewsSure*]
    [$Summary? $UnsureNews           $Topic           $NotNewsSure*]
    [$Summary? $UnsureNews   $Where? $AnyTopic?       $NotNewsSure* $AtTime]
    [$Summary? $UnsureNews   $Where? $AnyTopic?       $NotNewsSure* $Tell]
    [$Summary? $News+        $Where? $UnsureAnyTopic? $NotNewsSure*]
    [$Summary? $NewsIncident $Where?                  $NotNewsSure*]

    $WhatNewHappens
    %weight 100
    [$Summary? $News         $Where? $UnsureAnyTopic? $NotNews*]
    [$Summary? $NewsIncident $Where?                  $NotNews*]

    [$Summary? $News? $SmiTell                     $Smi $NotNews*]
    [$Summary? $News? $SmiWhat $Written $AtSource* $Smi $NotNews*]

    %weight 1000
    %lemma
    [$LentaOfNews            $Where? $UnsureAnyTopic? $NotNews*]

$LentaOfNews:
    %lemma
    лента $News


$WhatNewHappens:
    [$Summary? $What       $Happens $Where  $Topic? $NotNews* $News?]
    [$Summary? $What       $Happens $Where? $Topic  $NotNews* $News?]
    [$Summary? $What       $Happens $Where? $Topic? $NotNews* $News? $AtTime]
    [          $What  $New $Happens $Where? $Topic? $NotNews* $News?]
    [          $What  $New          $Where  $Topic? $NotNews* $News?]
    [          $What  $New          $Where? $Topic  $NotNews* $News?]
    [          $What  $New          $Where? $Topic? $NotNews* $News? $AtTime]
    $What $New $Preposition [$ArbitraryTopic $NotNews*]
$New:
    нового
    новенького
    интересного
$NotNewsSure:
    $AtTime
    $Adjective
    $CustomFillers
    $More
    $Preposition
    $Tell
    $What
$NotNews:
    ($Common.Play|$Tell)
    $Adjective
    $AtTime
    $CustomFillers
    $Happens
    $More
    $New
    $Preposition
    $What
$Summary:
    %lemma
    лента
    обзор
    сводка
$Content:
    $Where
    $Topic
$HappensInPast:
    произошло
    произошла
    произошли
    случились
    случилось
$Happens:
    $HappensInPast
    происходит
    случилось
    творится
$What:
    чё
    что
    что там
    что по
    что по поводу
    что про
    чего
    как
    %lemma
    какие
    какая
    какое
    какой
$SmiTell:
    пролистай
    пролистайте
    прочитать
    прочитай
    прочитайте
    прочти
    почитать
    почитай
    почитайте
    читать
    читай
    читайте
    узнать
    узнай
    узнайте
$SmiWhat:
    о чем
    что
$Written:
    пишут
    пишет
    написано
$AtSource:
    %lemma
    сайт
    сайта
    новостях
    сми

$Tell:
    $Common.PronounMe? $Common.Lets
    $Common.Repeat
    %lemma
    найди
    найдите
    включай
    включи
    включите
    включить
    воспроизведи
    воспроизведите
    высылай
    давай все таки
    давай послушаем
    давай расскажи
    давай поставь
    давай прочитай
    давай узнаем
    зачитай
    зачитайте
    запусти
    знаешь
    ищи
    ищите
    найди
    найдите
    открой
    откройте
    передай
    передайте
    переключи
    поговорим
    поищи
    поищите
    покажи
    покажите
    послушаем
    послушать
    посмотреть
    поставь
    поставьте
    поставить
    почитай
    почитайте
    прокомментируй
    продолжай
    прочитай
    прочитайие
    прочитать
    расскажи
    расскажите
    рассказать
    рассказывай
    рассказывайте
    (продолжай|продолжайте) рассказывать?
    скажешь
    скажи
    скажите
    слушать
    сообщи
    узнай
    читай
    читать
    что говорят
$MoreVerb:
    больше
    другие
    новые
    следующие
$More:
    еще
    еще раз
    еще $MoreVerb
    есть $MoreVerb
$WhatsUpNews:
    как там? дела
$UnsureNews:
    $News
    обстановка
    обстановку
    ситуация
    ситуацию
    ситуации
    вести
    события
$News:
    %lemma
    Новости
    Известия
    %weight 0.01
    Происшествия
$NewsIncident:
    %lemma
    %type "custom.news_topic"
    %value "incident"
    Происшествия
$Adjective:
    новые
    %lemma
    актуальные
    будешь
    быстрые
    важные
    голосовые
    другие
    основные
    первые
    подробные
    последнего
    последние
    самого
    самые
    сегодняшние
    свежего
    свежие
    свеженьких
    следующие
    твои
    топовые
    федеральные

    %weight 0.01
    главная
    главное
    главные
    интересные
    интересного
$UnsureAnyTopic:
    $Topic
    $ArbitraryTopic
    $UnsureArbitraryTopic
$AnyTopic:
    $Topic
    $ArbitraryTopic
$Topic:
    %weight 10
    $Smi
    $Rubric
    %weight 0.01
    $Fio
$Fio:
    [$sys.fio.surname $sys.fio.name? $sys.fio.patronym?]
$UnsureArbitraryTopic:
    %weight 0.01
    .*
    %negative
    $NotTopic
$ArbitraryTopic:
    . .? .?
    . фм
    . ру
    %lemma
    эхо москвы
    %negative
    $NotTopic
$NotTopic:
    [.* видео]
    [.* погода]
    [.* музыка]
    [смотреть? онлайн]
    геомагнитная
    канал
    навык
    на? дорогах
    нет
    номер
    отличные
    ру
    фм
    %lemma
    [.* (youtube|ютуб|ютуба)]
$Preposition:
    а
    в
    в сфере
    в области
    во
    то
    за
    ж
    же
    из
    или
    и
    на
    на тему
    ну
    о
    об
    по
    про
    с
    у
    %weight 0.001
    в мире
$Where:
    $WhereNoun
    #$WhereAdjective
    в городе
    %negative
    ссср
$WhereNoun:
    $ner.GeoAddr.City
    $ner.GeoAddr.City2
    $ner.GeoAddr.Province
    $ner.GeoAddr.Country
    $ner.GeoAddr.Address
    $Country
    $RegionRu
$AtTime:
    $sys.num* $sys.datetime
    %weight 100
    %lemma
    вчера
    вчерашний день
    сегодня
    последний день
    сегодняшний день
    с утра
    день
    последние? сутки
    (текущий|этот) час
    часа

$LeftSpeech:
    $CustomFillers
    выключи
    ладно
    лучше
    мое? солнышко мое?
    моя? милая моя?
    не музыка
    не видео
    нормально
    отключись
    перестань
    прекращай
    прекрати
    стоп
    хорошо
$CustomFillers:
    $AtTime
    $sys.swear
    бесплатно
    быстро
    быстрее
    вечерние
    вчерашние
    в сети
    в этом городе
    все
    выпуск
    где
    громкость на? $sys.num
    звук на? $sys.num
    громче
    да
    дня
    есть
    еще
    именно
    интересно
    кого
    короче
    любопытно
    мне
    меня
    месяца
    можешь
    можете
    нам
    на русском языке?
    не
    нет
    нету
    нибудь
    отключись
    очень
    погромче
    подробно
    пожалуйста
    пока
    понимаю
    просто
    прямо
    радио
    сейчас
    снова
    спасибо
    срочно
    та
    так
    такое
    там
    те
    тебе
    тебя
    ты
    теперь
    тогда
    тоже
    тот
    тут
    узнать
    утренние
    у вас
    у нас
    у тебя
    хоть
    часа
    что
    этого часа
    этот
    эту
    я
    %lemma
    гугл
    какая то
    они
    эта
    эти
    это
    яндекс

$RegionRu:
    %lemma
    адыгея
    алтай
    башкортостан
    бурятия
    дагестан
    ингушетия
    кабардино-балкария
    калмыкия
    карачаево-черкесия
    карелия
    коми
    крым
    марий эл
    мордовия
    саха
    якутия
    северная осетия-алания
    татарстан
    тыва
    удмуртия
    хакасия
    чечня
    чувашия
    алтайский край
    забайкальский край
    камчатский край
    краснодарский край
    красноярский край
    пермский край
    приморский край
    ставропольский край
    хабаровский край
    амурская область
    архангельская область
    астраханская область
    белгородская область
    брянская область
    владимирская область
    волгоградская область
    вологодская область
    воронежская область
    ивановская область
    иркутская область
    калининградская область
    калужская область
    кемеровская область
    кировская область
    костромская область
    курганская область
    курская область
    ленинградская область
    липецкая область
    магаданская область
    московская область
    мурманская область
    нижегородская область
    новгородская область
    новосибирская область
    омская область
    оренбургская область
    орловская область
    пензенская область
    псковская область
    ростовская область
    рязанская область
    самарская область
    саратовская область
    сахалинская область
    свердловская область
    смоленская область
    тамбовская область
    тверская область
    томская область
    тульская область
    тюменская область
    ульяновская область
    челябинская область
    ярославская область
$Country:
    %lemma
    австралия
    азербайджан
    соединенные штаты америки
    сша
    америка
    северная америка
    североамериканские штаты
    штаты
    армения
    белоруссия
    белорусь
    беларусь
    республика беларусь
    республика белорусь
    болгария
    бразилия
    великобритания
    англия
    британия
    шотландия
    северная ирландия
    соединенное королевство
    венгрия
    республика корея
    южная корея
    корея
    гонконг
    дания
    объединенные арабские эмираты
    оаэ
    эмираты
    египет
    индия
    индонезия
    иран
    исландия
    италия
    казахстан
    канада
    киргизия
    китай
    латвия
    литва
    малайзия
    мексика
    молдавия
    новая зеландия
    новозеландия
    зеландия
    израиль
    норвегия
    польша
    россия
    российская федерация
    рф
    раша
    рашка
    румыния
    саудовская аравия
    сингапур
    словакия
    таджикистан
    таиланд
    турция
    туркмения
    узбекистан
    узбекистон
    узбекистонда
    украина
    филиппины
    хорватия
    чехия
    швеция
    швейцария
    эстония
    южная африка
    япония
$WhereAdjective:
    %lemma
    'адыгейские'(where:адыгея)
    'алтайские'(where:алтай)
    'башкортостанские'(where:башкортостан)
    'башкирские'(where:башкирия)
    'бурятские'(where:бурятия)
    'дагестанские'(where:дагестан)
    'калмыкские'(where:калмыкия)
    'карельские'(where:карелия)
    'крымские'(where:крым)
    'мордовские'(where:мордовия)
    'якутские'(where:якутия)
    'осетинские'(where:"северная осетия-алания")
    'удмуртские'(where:удмуртия)
    'чеченские'(where:чечня)
    'чувашские'(where:чувашия)
    'забайкальские'(where:"забайкальский край")
    'камчатские'(where:"камчатский край")
    'краснодарские'(where:краснодар)
    'красноярские'(where:красноярск)
    'пермские'(where:пермь)
    'приморские'(where:"приморский край")
    'ставропольские'(where:ставрополь)
    'хабаровские'(where:хабаровск)
    'амурские'(where:"амурский край")
    'архангельские'(where:архангельк)
    'астраханские'(where:астрахань)
    'белгородские'(where:белгород)
    'брянские'(where:брянск)
    'владимирские'(where:владимир)
    'волгоградские'(where:волгоград)
    'вологодские'(where:"вологодская область")
    'воронежские'(where:воронеж)
    'ивановские'(where:"ивановская область")
    'иркутские'(where:иркутск)
    'калининградские'(where:калининград)
    'калужские'(where:калуга)
    'кемеровские'(where:кемеров)
    'кировские'(where:киров)
    'костромские'(where:кострома)
    'курганские'(where:курган)
    'курские'(where:курск)
    'ленинградские'(where:"санкт-петербург")
    'липецкие'(where:липецк)
    'магаданские'(where:магадан)
    'московские'(where:москва)
    'мурманские'(where:мурманск)
    'нижегородские'(where:"нижний новгород")
    'новгородские'(where:новгород)
    'новосибирские'(where:новосибирск)
    'омские'(where:омск)
    'оренбургские'(where:оренбург)
    'орловские'(where:орел)
    'пензенские'(where:пенза)
    'псковские'(where:псков)
    'ростовские'(where:ростов)
    'рязанские'(where:рязань)
    'российские'(where:россия)
    'самарские'(where:самара)
    'саратовские'(where:саратов)
    'сахалинские'(where:сахалин)
    'свердловские'(where:свердловск)
    'смоленские'(where:смоленск)
    'тамбовские'(where:тамбов)
    'тверские'(where:тверь)
    'томские'(where:томск)
    'тульские'(where:тула)
    'тюменские'(where:тюмень)
    'ульяновские'(where:ульяновск)
    'челябинские'(where:челябинск)
    'ярославские'(where:ярославль)
