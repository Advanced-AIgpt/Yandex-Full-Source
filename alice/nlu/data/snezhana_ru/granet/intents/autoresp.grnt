form snezhana.autoresp_busy:
    root:
        $User? [$Busy+ $LeaveMessage*] $Anything*
    filler:
        $Filler

form snezhana.autoresp_not_answered:
    root:
        $User? [$NotAnswered+ $LeaveMessage*] $Anything*
        $Anything* ($User | $LeaveMessage)+ $Anything*
    filler:
        $Filler

form snezhana.autoresp_unavailable:
    root:
        $User? [$Unavailable+ $LeaveMessage*] $Anything*
        услуга заблокирована
    filler:
        $Filler

form snezhana.autoresp:
    root:
        $Greet? $Autoresp $Anything*
        $Anything* $Autoresp $Anything*
    filler:
        $Filler
        $PronounFiller


$UserPrefix:
    вызываемый
    данный
    набранный вами
    телефонный

$UserSuffix:
    %lemma
    номер
    абонент
    номер абонента
    аппарат вызываемого? абонента
    автоответчик
    голосовой ящик

$User:
    $UserPrefix* $UserSuffix+

$Busy:
    занят

$Unavailable:
    недоступен
    выключен
    отключен
    свободен
    заблокирован
    изменился
    не существует
    не подключен
    не обслуживается
    не активен
    не используется
    не обслуживается
    больше
    больше не
    больше не принадлежит
    находится во временной
    находится во временной блокировке
    временно заблокирован
    свободен для его покупки


$NotAnswered:
    не отвечает
    временно недоступен
    не может ответить
    не можем ответить

$Autoresp:
    продолжается? попытка дозвониться
    соединение установлено
    вы (звоните | дозвонились | позвонили | обратились) (в | на горячую линию | по телефону)?
    введите
    нажмите
    наберите
    вы? можете отправить факс
    для соединения
    все? (операторы | линии | консультанты) заняты?
    звонок переведен на
    звонок будет направлен
    дождитесь ответа оператора
    ответит (первый | 1) свободный сотрудник
    оператор ответит
    в ближайшее время ответит
    оставайтесь на линии
    дождитесь ответа
    ожидайте ответа
    информируем что разговор записывается
    информируем (о том | что)
    в целях (улучшения | повышения) качества? обслуживания
    продолжая разговор вы соглашаетесь
    звонок очень? важен
    звонок (является | принят)
    очень важен звонок
    очень? рады каждому?
    благодарим вас? за (звонок | обращение)
    вас приветствует голосовой помощник
    мы не работаем
    мы принимаем звонки
    $LeaveMessage
    $User

$LeaveMessage:
    оставьте
    (оставьте | можете оставить) сообщение
    (оставьте | можете оставить) сообщение на (автоответчик | после | после сигнала)
    говорите после сигнала

$Filler:
    к? сожалению
    (на | в) данный момент
    пожалуйста
    сейчас
    этот
    нет

$PronounFiller:
    %lemma
    вы
    мы
    ваш
    наш

$Anything:
    .+

$Greet:
    %lemma
    алло
    здравствуй
    здравствуйте
    с добрым утром
    доброе утро
    добрый день
    добрый вечер
